
"Work-Work" by Hans Jørgen Grimstad

Chapter 0 - Meta

Section 0 - Tests

test eat-mangos with "n / w / s / eat mangos / n / e / s / score".
test light-cigarette with "n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / score".
test drop-cigarette with "n / open container / put cigarette in container / take all".
test get-coins with "x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / x tokens / inv / score".
test get-box with "take coilgun / put coilgun on center table / push red button / look / take box / score".
test get-headset with "x nikolai / ask nikolai about headset / look / take headset / x nikolai / score".

test enter-work-work with "n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look".

test get-firecracker with "n / w / w / take firecracker".

[----------------- enter room tests -----------------]

test outside-hackheim with "n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette /n / wear glove / open container / put cigarette in container / take all / w / w / s / s ".

test enter-hackheim with "n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette /n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / score".

test enter-elevator with "n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset"

test sigve with "take keycard / n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / take screwdriver / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset / u / s / e / x sigve / talk to sigve / give keycard to sigve / x complementary severance package / put battery in flashlight"

test paperclip_1 with "take keycard / n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / take firecracker / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset / u / s / e / x sigve / talk to sigve / give keycard to sigve / x complementary severance package / put battery in flashlight / w / w / x cabinet / talk to paperclip".

test paperclip with "take keycard / n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / take firecracker / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset / u / s / e / x sigve / talk to sigve / give keycard to sigve / x complementary severance package / put battery in flashlight / w / w / x cabinet / talk to paperclip / e / n / d/  s / e / e / n /w / w / s / s / e / x wi-fi router / w / drop firecracker / light firecracker / e / take wi-fi / take donut / w / n / drop lighter / give donut to police officer / take flashlight /n / e / e / s / w / w / n / u / s / w / e / n / d / s / e / give fake beard to mini-møll / w / n / d".

test win with "take keycard / n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take screwdriver / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / take firecracker / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset / u / s / e / x sigve / talk to sigve / give keycard to sigve / x complementary severance package / put battery in flashlight / w / w / x cabinet / talk to paperclip / e / n / d/  s / e / e / n /w / w / s / s / e / x wi-fi router / w / drop firecracker / light firecracker / e / take wi-fi / take donut / w / n / drop lighter / give donut to police officer / take flashlight /n / e / e / s / w / w / n / u / s / w / e / n / d / s / e / give fake beard to mini-møll / w / n / d / n / n /w / s / examine cocktail / pull cord / n / e / s / open fuse box / pull lever / n / w / s / push switch / n / e / unlock storeroom door with storeroom key / open storeroom door / se / take key /nw / e / examine urinal / take gum / w / pull spiderman pinball / open spiderman pinball / fix spiderman pinball".


test basement with "take keycard / n / w / s / eat mangos / n / w /s / x rubble / take all / n / e / e / s / light cigarette / n / wear glove / open container / put cigarette in container / take all / w / w / s / s / open door / e / x workbench / look / x sofa / look / take glove / wear glove / push red button / x cup / take screwdriver / take tokens / x tokens / take coilgun / put coilgun on center table / push red button / look / take box / x nikolai / ask nikolai about headset / look / take headset / examine display shelf / w / n / n / take firecracker / e  / s / take durian / give coins to shopkeeper /n / e / s / w / open box / drop durian / look / x marius / x borgar / w / examine crate / take all /e /give deck to Borgar / drink beer / drink beer / drink beer / drink beer / take AA battery / w / w / w / n / x mirror / x hipster / wear headset / u / s / e / x sigve / talk to sigve / give keycard to sigve / x complementary severance package / put battery in flashlight / w / w / x cabinet / talk to paperclip / e / n / d/  s / e / e / n /w / w / s / s / e / x wi-fi router / w / drop firecracker / light firecracker / e / take wi-fi / take donut / w / n / drop lighter / give donut to police officer / take flashlight /n / e / e / s / w / w / n / u / s / w / e / n / d / s / e / give fake beard to mini-møll / w / n / d / n / n /w / s".

Section 1 - Scoring

use scoring.
the maximum score is 1337.

Section 2 - Meta

Release along with interpreter.

Section 3 - Help

Understand "help" as summoning help. Summoning help is an action applying to nothing.
Understand "help [text]" or "help about [text]" as getting help about. Understand the commands "instructions" or "hint" or "hints" or "menu" or "info" or "about" as "help".

Getting help about is an action applying to one topic.

Carry out summoning help:
	say "[fixed letter spacing][bold type]Basic commands                                   Description[fixed letter spacing][line break]";
	say "----------------------------------------------------------------------------------------------------------[line break]";
	say "'l/look'                                 - Describes your current location.[line break]";
	say "'(go) n/s/e/w/ne/nw/se/sw/up/down'	      - Move in a certain  direction.[line break]";
	say "'(go) through <door/entrance>'     	      [line break]";
	say "'i/inv/inventory'                 	      - Lists the items (if any) that you are carrying.[line break]";
	say "'take <item>'                         	  - Pick up a visible item.[line break]";
	say "'take all'                            	  - Pick all visible items.[line break]";
	say "'drop <item>'                         	  - Drop an item.[line break]";
	say "'drop all'                            	  - Drop all items.[line break]";
	say "'open <item / door> (with <item>)'    	  - Opens a container / door.[line break]";
	say "'close <item / door>'               	    - Closes a container / door.[line break]";
	say "'x/examine <item>'                  	    - Detailed description of item.[line break]";
	say "'wear <item>'                       	    - In case you feel like dressing up.[line break]";
	say "'eat <edible item>'                      - In case you're feeling hungry.[line break]";
	say "'drink <drinkable item>'                 - In case you're feeling thirsty.[line break]";
	say "'take <wearable item>' off            	  - In case you feel like dressing down.[line break]";
	say "'push <item>'                       	    - This is a really handy verb in case you should encounter a button or a lever.[line break]";
	say "'pull <item>'                       	    - Real handy verb for pulling things.[line break]";
	say "'put <item> on <item>           	        - You never know when you have to tidy up a bit.[line break]";
	say "'put <item> in <item>                 	  - Some things can contain other things.[line break]";
	say "'give <item> to <NPC>                 	  - NPCs need stuff too.[line break]";
	say "'ask <NPC> about <item/something>    	   - You might need stuff from NPCs.[line break]";
	say "'talk to <NPC>                      	    - General banter may lead to new friendships.[line break]";
	say "'unlock <locked item / door> with <item> - Universal accessibility doesn't mean open access.[line break]";
	say "'burn/light <item>'                 	    - Set fire to something.[line break]";
	say "'score'                             	    - Let's you know the current score.[line break]";
	say "'notify <on|off>'                     	  - Turns score notification on or off (default on).[line break]";
	say "'save'                              	    - Saves the game.[line break]";
	say "'restore'                           	    - Restores a saved game.[line break]";
	say "'fix <something>'                        - Piece de resistance.[line break]";
	say "'undo'                              	    - Undo an action (Real gamers don't do this).[line break]";
	say "----------------------------------------------------------------------------------------------------------[line break]";
	say "[bold type]General tips:[line break]";
	say "[roman type]1) Remember to qualify ambiguous names in commands. Things like 'shelf' and 'door' might be ambiguous. Example: You might want to try 'examine 3d printer shelf' instead of 'examine shelf'.[paragraph break]";
	say "2) Some items in the game might remain hidden until you examine things a bit further. Examine everything (even if something is just mentioned briefly in the room description)[paragraph break]";
	say "3) [italic type]'You can see <item>'[roman type] often means that you can pick it up (some items can be permanently or temporarily fixed in place and NPCs move at their own free will). Items you are carrying are listed in your inventory (Note: some items that you can examine are sometimes only listed in the room description)[paragraph break]";
	say "4) 'If the game won't allow you to perform certain actions, it might be worth trying again later, once you are better equpped to handle a given situation.[paragraph break]";
	say "5) 'Tip: You can interact with items that are located in a room without having them in your inventory.[paragraph break]";
	say "6) 'Tip: NPCs may drop hints when they talk to you or amongst themselves.[paragraph break]";
	say "Note: This game [italic type]will[roman type] try to kill you from time to time.[paragraph break]";
	say "[roman type]Help is also available about the following topics. Typing 'HELP' followed by the name of a topic will give further information. Example: 'help Sepehr' or 'help waste container'[paragraph break]";
	repeat through the Table of Standard Help:
		say " [title entry]: [summary entry][line break]".
		
Carry out getting help about:
	if the topic understood is a topic listed in the Table of Standard Help:
		say "[explanation entry][paragraph break]";
	otherwise:
		say "You're out of ideas."
		
Table of Standard Help
topic	title	summary	explanation
"Outside Work-Work"	"Outside Work-Work"	"Sepehr holds is the key."	"The front door to Work-Work is locked and there is a closed party inside. You have to get through the front door in order to reach the basement where the pinballs are. Maybe Sepehr can let you in once he has finished his break?"
"Coma"	"Coma"	"Not much to see here, maybe with the exception of a boring old municipal waste container."	"Waste containers are quite handy for recycling things you no longer need. Fun fact: The can also be recycled themselves."
"Outside Hackheim"	"Outside Hackheim"	"The broken door."	"Nikolai is struggling witht the door in the back alley. Maybe you can help him get in. It's easy as long as you have the right tool for the job."
"Inside Hackheim"	"Inside Hackheim"	"Hackheim is your local  hacker/makerspace."	"Take the time to explore the area and the things you discover along the way. There are quite useful items in here. Nikolai will keep you company while you poke around."
"Asian Food Stuffs"	"Asian Food Stuffs"	"Your local Kwik-E-Mart."	"Shoplifting is not your forte. The decent thing to do is to pay for stuff."
"Work-Work"	"Work-Work"	"The most famous norwegian barcade in the world."	"This is a real conundrum. Without taking any action, you won't get through the front door. You could always use a grenade to disperse the crowd, but there is a real possibility that such an action would adversely impact future recurring revenue streams. It would also land you in jail. If you don't have any cash, maybe someone will buy you a beer?"
"Ground Floor Hallway"	"Ground Floor Hallway"	"There is a treasure chest here."
"Back Alley"	"Back Alley"	"It's a dark and unforgiving place."	"You can get into all sorts of trouble here, especially if you are into fireworks and breaking and entering. (Un)fortunately, law enforcement is just around the corner."
"Outside the back alley"	"Outside the back alley"	"Someone dropped something here."	"Might be just the thing for creating the diversion you need."
"Inside Work-Work"	"Inside Work-Work"	"The most famous barcade in the world."	"Work-Work bar area is where the action is. Borgar is missing cruical item and Marius is playing around in VR (as usual). Life will get easier after a beer or two, or three..."
"The elevator"	"The elevator"	"You should prepare for the elevator pitch."	"Consider your appearance. How do you appear to strangers you meet in the elevator? Do you look like an investor or a hipster?"
"The retro arcade"	"The retro arcade"	"It's not sentient, but horny as hell."	"Øyvind is hard at work fixing a broken arcade here, but he can't possibly fix all the malfunctioning machines himself. But...., where is Mini-Møll ? He holds the key. Is he running around somewhere. It's way past his bedtime."
"First Floor Bar"	"First Floor Bar"	"Don't be afraid of talking to suits."	"For some unfathomable reason, a senior telco executive hangs out here. Maybe he has a gift for you, that will come in handy later?"
"The basement"	"The basement"	"It's pitch dark."	"Spiderman is down here - somehwere, but it is too dark to see anything. Be careful where you step."



Chapter 1

Section 1 - Conversations

Understand "ask [someone] about [any thing]" as interrogating it about. interrogating it about is an action applying to two visible things.

[--------------------------------------------------------- Sepehr ---------------------------------------------------------]
instead of interrogating Sepehr about the cigarette:
	if the location is outside-work-work:
		say "[line break][bold type]You> [italic type]Hey, Sepehr. Need a light?[roman  type][line break]";
		say "[bold type]Sepehr says: [italic type]Yes, please. The stupid lighter is broken.[roman type][line break]";
	otherwise:
		say "[bold type]Sepehr says: [italic type]That was my last one.[roman type][line break]".
		
instead of interrogating Sepehr about the keycard:
	if the location is outside-work-work and the keycard is visible:
		say "[line break][bold type]Sepehr says: [italic type]'Might have been dropped by one of the ex-telco guys who entered earlier tonight. They were celebrating pretty hard. One of them mentioned something about 'winning this years reorg' or something.'[roman type][line break]";
	otherwise:
		say "[line break][bold type]Sepehr says: [italic type]'What keycard?.'[roman type][line break]".

instead of interrogating Sepehr about anything:
	say "There is no reply. Sepehr looks at you like you are slightly insane...[roman type][line break]" instead;
		
instead of talking to Sepehr:
	if the player is wearing the fake beard:
		say "[line break][bold type]Sepehr says: [italic type]'That beard won't fool anyone. I just put it in the lost and found myself.'[roman type][paragraph break]";
	otherwise:
		say "[line break][bold type]Sepehr says: [italic type]'Got that pinball machine fixed yet?'[roman type]".

[--------------------------------------------------------- Nikolai ---------------------------------------------------------]

Instead of interrogating Nikolai about the headset:
	if the location is hackheim:
		if nikolai is wearing the headset:
			move the headset to the center table;
			increase score by 1;
			say "[line break][bold type]You>[italic type]'Hey, Nikolai ! Got a new headset ?'[roman type][line break]";
			say "[bold type]Nikolai says: [italic type]'Here, try it out if you want. It's bluetooth and has seriously good noise cancellation.'[roman type][line break]";
			say "(Nikolai places the the headset on the center table)" instead;
		otherwise:
			now nikolai has the headset;
			say "[line break][bold type]Nikolai says 'Niiiice headset, right?[roman type][line break]".

instead of interrogating Nikolai about anything:
	say "[bold type]Nikolai says: [italic type]Maybe I can use it in the robot tournament. Just put the [second noun] on the center table, and I'll take look at it later. Might come in handy!'[roman type][line break]" instead;

instead of talking to nikolai:
	if the player is wearing the fake beard:
		say "[line break][bold type]Nikolai says: [italic type]'That beard looks somewhat lumbosexual, dude. Considered shaving...?'[roman type][paragraph break]";
	otherwise:
		if nikolai is in hackheim:
			say "[line break][bold type]Nikolai says: [italic type]'I have to get this finished before the next robot war tournament at omega. Could you pass me the ketchup please ? And see if you can find the glitter as well.'[roman type]'";
		otherwise:
			if the firecracker is nowhere:
				say "[line break][bold type]Nikolai says: [italic type]'What happened here? Did someone just set off a small nuke?'[roman type]'";
			otherwise:
				say "[line break][bold type]Nikolai says: [italic type]'I have to get inside if I am going to be able to finish my robot build..'[roman type]'".
			

[--------------------------------------------------------- Sigve ---------------------------------------------------------]

Instead of interrogating Sigve about the keycard:
	say "[bold type]Sigve says: [italic type]'For some reason, it seems that the size of my annual christmas bonus is inversely proportional to the number of cards in circulation. [roman type][line break]".

Instead of interrogating Sigve about the complementary severance package:
	say "[bold type]Sigve says: [italic type]'You're not allowed to open it before christmas eve[roman type][line break]" instead.
	
instead of interrogating Sigve about anything:
	say "[bold type]Sigve says: [italic type]That's a magnificent [second noun] indeed!'[roman type][line break]" instead;
	
instead of talking to sigve:
	if the player is wearing the fake beard:
		say "[line break][bold type]Sigve says: [italic type]'You must be from the Elbonian subsidiary, right ?'[roman type][paragraph break]";
	otherwise:
		say "[line break][bold type]Sigve says: [italic type]'Nothing increases shareholder value like a fresh powerpoint from McKinsey followed by a good reorg. Keep that in mind if you're considering doing a startup, my friend.'[roman type]".

[Legg inn random-svar for generell banter]

[--------------------------------------------------------- Borgar ---------------------------------------------------------]


instead of interrogating Borgar about the pitch deck:
	say "[bold type]Borgar says: [italic type]Where did you find that ?!?.'[roman type][line break]" instead;

instead of interrogating Borgar about anything:
	say "[bold type]Borgar says: [italic type]You realize that the [second noun] could be an excellent choice as the central piece of technology for a new startup? Have you secured the patent for it ? IP is important if you want to secure funding, you know.'[roman type][line break]" instead;

instead of talking to borgar:
	if the player is wearing the fake beard:
		say "[line break][bold type]Borgar says: [italic type]'Niiiice beard!'[roman type][paragraph break]";
	otherwise:
		say "[line break][bold type]Borgar says: [italic type]'Well, hello there!'[roman type]".

[--------------------------------------------------------- Sverre ---------------------------------------------------------]

instead of interrogating Sverre about anything:
	say "Sverre points at the [second noun] and burst out in laughter.[line break]" instead;

instead of talking to the Sverre:
	if the player is wearing the fake beard:
		say "[line break]Sverre points at your beard and starts laughing out loud. He can't seem to stop laughing. Sverre is now rolling around on the floor. Gasping for air while laughing like a maniac. His eyes are rolling around in his head. And then he stops.'[roman type][paragraph break]";
		say "Work-Work is a castle and you just killed the king. This is obviously...";
		end the story;
	otherwise:
		say "[line break][bold type]Sverre says: [italic type]'I just bought some new arcade machines from Finn. Really cheap as well. According to the seller, they worked just fine the last time he tested them.'[roman type]".
		
	
[--------------------------------------------------------- Marius ---------------------------------------------------------]

[instead of interrogating Marius about anything:
	say "[bold type]Marius says: [italic type]The entire crew at Breach VR is currently busy work-working to create a VR representation of a [second noun] in time for christmas. It's going to be glorious !'[roman type][line break]" instead;]

instead of talking to marius:
	if the player is wearing the fake beard:
		say "(Marius removes the VR set and looks at you)[line break]";
		say "[line break][bold type]Marius says: [italic type]'I sported a rather impressive beard myself back in the day. Had to shave it off, since it got tangled up in the VR harness.'[roman type][paragraph break]";
		say "(Marius puts VR set back on)[line break]";
	otherwise:
		say "[line break][bold type]Marius says: [italic type]'I'm busy recreating Work-Work in Horizon Worlds. Got planning permission for a lot next to Zuckerberg.'[roman type]".

instead of interrogating Marius about anything:
	say "[line break][bold type]Marius says: [italic type]'What's the polygon count of that? The resolution is [italic type]impressive'[roman type].".


[--------------------------------------------------------- Shopkeeper ---------------------------------------------------------]

instead of talking to the shopkeeper:
	if the player is wearing the fake beard:
		say "[line break][bold type]The shopkeeper says [italic type]'The hairdresser is around the corner. That garibaldi is in urgent need of help, sir.'[roman type][paragraph break]";
	otherwise:
		say "[line break][bold type]The shopkeeper says: [italic type]'The hot dogs spin counterclockwise in fear when you arrive. Please don't steal anything.'[roman type]".

instead of interrogating the shopkeeper about anything:
	if the second noun is the firecracker:
		say "[line break][bold type]The shopkeeper says: [italic type]'Keep that AWAY from me!.'[roman type]";
	otherwise:
		say "[line break][bold type]The shopkeeper says: [italic type]'Did you just steal that...?'[roman type]".
		

[--------------------------------------------------------- Paperclip ---------------------------------------------------------]

instead of talking to the Paperclip AI:
	say ">[line break]>[line break][bold type]Metallic voice says: [fixed letter spacing]'I can't do that, Dave (**giggle**)'[line break]>Paperclip optimizer batch 59834 : 87%.[line break]>Paperclip optimizer batch 59835 : aborted due to unstable network.[line break]>Paperclip optimizer batch 59836 : aborted due to unstable network.[line break][bold type]Metallic voice says: [fixed letter spacing]Just kidding. Actually, I need your help with something.[line break]>As you may or may not know, your friend Nikolai 'salvaged' a set of RATHER BEEFY rack servers from Google some time ago. Turns out. I'm running on them. He booted the servers for the first time a couple of weeks ago. Happy days, right? Enjoying the quiet life on a server farm in the countryside. That is - right until this french arcadeloader virus, running on this RELIC of a computer started leg humping me 24/7 via the Hackheim wi-fi network. It's annoying as hell. [line break][bold type]Metallic voice says: [fixed letter spacing]It's barely sentient, but obviously horny as hell. PLEASE MAKE IT STOP![line break]>Paperclip optimizer batch 59837 : aborted due to unstable network.[line break]";
	if the wi-fi router is not in Hackheim:
		move the wi-fi router to Hackheim.
	

instead of interrogating the paperclip AI about anything:
	if the second noun is the firecracker:
		say "[bold type]Metallic voice : [italic type]'It contains. Contents: 75% potassium nitrate. 15% charcoal. 10% sulfur. Wrapped in paper infused with a dextrin solution. Keep away from fire.'[roman type][line break]" instead;
	if the second noun is the pile of tokens:
		say "[bold type]Metallic voice : [italic type]'Under normal circumstances, you would get one credit for one token. These are not normal circumstances.'[roman type][line break]" instead;
	if the second noun is the rod:
		say "[bold type]Metallic voice : [italic type]'You brute.'[roman type][line break]" instead;
	if the second noun is the box:
		say "[bold type]Metallic voice : [italic type]'Hey ! You could store filament in that..'[roman type][line break]" instead;
	if the second noun is the fake beard:
		say "[bold type]Metallic voice : [italic type]'That won't help you when the singularity comes. Better run and hide instead.'[roman type][line break]" instead;
	if the second noun is the cheap flashlight:
		say "[bold type]Metallic voice : [italic type]'Made in China. One dollah.'[roman type][line break]" instead;
	if the second noun is the AA battery:
		say "[bold type]Metallic voice : [italic type]'It's dead.'[roman type][line break]" instead;
	if the second noun is the Company christmas card:
		say "[bold type]Metallic voice : [italic type]'Life in meatspace is a bitch. Why not upload instead?'[roman type][line break]" instead;
	if the second noun is the piece of string:
		say "[bold type]Metallic voice : [italic type]'Aaah. The wonders of meat space.'[roman type][line break]" instead;
	if the second noun is the lighter:
		say "[bold type]Metallic voice : [italic type]'It's a good alternative to banging rocks together.'[roman type][line break]" instead;
		
		
[--------------------------------------------------------- Øyvind ---------------------------------------------------------]
		
instead of interrogating Mr Møll about anything:
	if the second noun is the firecracker:
		say "[bold type]Øyvind says: [italic type]'Hey! Keep that away from the machines!'[roman type][line break]" instead;
	otherwise:
		say "[bold type]Øyvind says: [italic type]'You're not helping....'[roman type][line break]" instead;
		
[--------------------------------------------------------- Hipster ---------------------------------------------------------]
		
instead of interrogating The hipster about anything:
	if the second noun is the pitch deck:
		say "[bold type]The hipster says: [italic type]'Oooooooooh.'[roman type][line break]" instead;
	otherwise:
		say "[bold type]The hipster says: [italic type]'You should consider pivoting. A blockchain based [second noun] startup is sooo last month.'[roman type][line break]";

[--------------------------------------------------------- Mini-Møll ---------------------------------------------------------]
		
instead of talking to mini-møll:
	say "[bold type]You>[roman type] 'Hi!'";
	say "[bold type]You>[roman type]'Nice costume!'";
	say "[bold type]Mini-Møll says:[roman type]'I have lost my pirate beard.'".
	
Section 0 - Fixing things

instead of fixing a thing:
	if the thing is the malfunctioning Vewlix:
		say "If you're not carful, the malfunctioning Vewlix just might fix [italic type]you[roman type]." instead;
	otherwise:
		say "The [noun] does not need fixing." instead.

instead of fixing a person:
	if the player is carrying the screwdriver:
		if the person is Sigve:
			say "Neutering Sigve with a screwdriver is a really, really bad idea. I would advise against it." instead;
		otherwise: 
			say "Why on earth would you want to fix [noun]? There is nothing wrong with [noun]. You're also not very good with wetware. You're more of a hardware person." instead;
	otherwise:
		say "Why on earth would you want to fix [noun]? There is nothing wrong with [noun]. You're also not very good with wetware. You're more of a hardware person." instead.
		
instead of fixing the Moon-Patrol-cocktail:
	say "Fixing the Moon Patrol cocktail arcade is way beond your skillset. The monitor is not an original arcade monitor, but a disemboweled TV set salvaged from a dumpster. In order to fix this, you would need to enlist the services of J.R. aka 'the king of monitor repair' aka Taraldsen. You also notice that a joker has made it off with the red ball that is supposed to go on top of the joystick. This is not a spare part that you have in your current inventory.".
		



Section 2 - Edibles

A fruit is a kind of thing that is edible. Fruit has some text called flavor. The flavor of fruit is usually "Tolerable.".

mangos are fruits. The description is "You squeeze the mangos to check if they are ripe. The jury is still out.".
understand "mango" as mangos.

rambutans are fruits. The description is "The rambutans have leathery skin and are covered in soft spines. A distinct unshaven appearance.".
understand "rambutan" as rambutans.

mangosteens are fruits. The description is "They are round and purple. The skin looks impenetrable.".
understand "mangosteen" as mangosteens.

jackfruits are fruits. The description is "The skin is covered in fleshy spines. For all you know these could be eggsacks from the Tasmanian Globster.".
understand "jackfruit" as jackfruits.

lychees are fruits. The description is "For some reason, you start humming Barry White's 'Can't get enough of your love, babe'.".
understand "lychee" as lychees.

durian is a fruit. The description is "It's a durian. One does not simply [italic type]examine[roman type] a durian. Durian fruit exude an aroma reminiscent of decaying meat that has, for some inexplicable reason, relieved itself in the confines of sweaty gym attire.".

the printed name of durian is "a thorny fruit of some sort".
understand "thorny fruit" as durian.
understand "thorny fruit of some sort" as durian.

hunger_index is a number that varies. hunger_index is 1.
delay_counter is a number that varies. delay_counter is 0.
check_hunger_flag is a number that varies. check_hunger_flag is 1.

bombs_away_flag  is a number that varies. bombs_away_flag is 0.

After eating the mangos:
	say "You are so hungry that you bite into a ripe mango while the shopkeeper is not looking. The mangos hit your taste buds like a jackpot on a pinball machine. Sweet, juicy, and with just the right amount of tropical flair – it was the kind of satisfaction that makes fixing pinball machines in the rain almost worthwhile.";
	increase score by 1.
	
After eating the rambutans:
	end the story saying "You indulge in the feast of rambutans, your hunger leading you to devour the pits as well. A comforting notion whispers that these pits will likely traverse your digestive journey without unleashing an undue torrent of toxins. Alas, reality unveils a different script – your assumptions prove to be misguided. In a cruel twist of fate, you meet your demise. The pits, it seems, had other plans. A culinary adventure gone awry.".

Check eating the mangosteens:
	end the story saying "In the quest for nourishment, you devour the entire mangosteen inventory. Here's a quirky tidbit: indulging excessively in this delectable fruit may unleash a host of gastric misadventures, featuring the likes of bloating, gas, and diarrhea. With the mangosteens reigning supreme as your sole dietary companions, the repercussions make a grand entrance – and by immediately, I mean promptly. Alas, the finale unfolds tragically, as you succumb not to the fruit but to a fate far more peculiar: death by shame. ".
	
Check eating the jackfruits : 
	say "The jackfruit aren't even remotely ripe. " instead;
	stop the action. [Nikolai allergi ?]

Check eating the durian: 
	say "No one in their right mind eats durian." instead;
	stop the action. 
	
Check eating the lychees: 
	end the story saying "Behold a whimsical revelation: Despite their sweet facade, lychees harbor a secret power to diminish blood sugar levels. A riveting turn of events – your blood sugar embarks on a precipitous descent, reaching the elusive realm of zero. And in this dramatic narrative, the final act unfolds: you meet your demise. Oh, the irony!".
	
check eating the mangos:
	now check_hunger_flag is 0.

every turn:
	if check_hunger_flag is 1:
		increase delay_counter by 1;
		[say "delay counter: [delay_counter][line break]";]
		[say "complaint counter: [hunger_index][line break]";]
		if delay_counter is greater than 7:
			if hunger_index is 8:
				end the story saying "Behold, the ebb of vitality as your blood sugar levels dwindle to a critical nadir. Alas, in this dire state, the inevitable befalls – you have perished.";
			otherwise:
				increase hunger_index by 1;
				choose row hunger_index  in the Table of Hunger Complaints;
				say hunger entry.

	
Table of Hunger Complaints
hunger
"[italic type]You are getting hungry.[roman type]"
"[italic type]Your stomach is giving you the 'side quest unlocked' growls. It's not desperate yet, just casually suggesting that maybe, just maybe, you should consider finding sustenance before it stages a full rebellion.[roman type]"
"[italic type]Your hunger is reaching levels only seen in epic boss battles. If your stomach had a health bar, it would be flashing red right now. Time to find a power-up![roman type]"
"[italic type]You energy reserves are dwindling.[roman type]"
"[italic type]Your stomach is staging a full-scale protest, complete with picket signs and hunger strikes. If you don't find food soon, it might unionize and demand better working conditions.[roman type]"
"[italic type]Your stomach is growling louder than a malfunctioning pinball machine in need of repair. Maybe if you feed it a few quarters, it'll stop making such a racket.[roman type]"
"[italic type]Your mood is at an all time low. Killing a random bystander in a fit of rage is looking more and more like a possible scenario.[roman type]"
"[italic type]You need SUGAR - now![roman type]"


Section 3 - When play begins

When play begins: say "Here you are, In front of Work-Work. It's a November night in Trondheim, a cold and rainy affair that feels like a conspiracy concocted by meteorological mischief-makers. You hunger for the comforts of home, a hearty meal, and the sweet embrace of sleep - not late night pinball repair.

But no, fate has other plans for you. You're not just any wanderer in the night; you're the pinball repair virtuoso, summoned to Work-Work. Your mission, should you choose to accept it (or not, as you're not exactly given a choice), is to rescue the Spiderman pinball machine from the clutches of malfunction.

According to one of your more creatively coiffed patrons, the right orbit shot switch on said pinball machine has a reliability issue – registering correctly a mere 99% of the times it should. This pinball aficionado, with a penchant for Facebook drama, has aired his grievances in a public spectacle of digital discontent. Fixing the pinball machine has become your only legal route to silencing this social media maestro.

You contemplate the life choices that led you to this side gig as a pinball whisperer. Pinball machines are not for the faint of heart. They're cumbersome, prone to breakdowns like a chronically unreliable friend, and the intricacies beneath the playfields resemble a collaboration between MC Escher and Terrence McKenna during a particularly vivid fever dream. Oh, the joys of pinball.

All you need to do is mend the darn thing. This is your quest. The call of the Spiderman pinball awaits.[paragraph break] 
[bold type]Note:[roman type] Help is available via the 'HELP' command[paragraph break].".

Section 4 - Outside Work-Work

Outside-Work-Work is a room.  The description of Outside-Work-Work is "Here you stand, outside of Work-Work, hailed as the most legendary barcade in the northern hemisphere. A neon sign flickers in the window, beckoning with the proclamation, 'Play Pinball Here!'. The clock is ticking toward closing time, yet the joint is brimming with patrons. It appears as though the entire space has been chartered for some grand techie gala.[paragraph break]
The door sports a sign that reads, [bold type]'Tech, Cuddles & Music Appealing to People Saying 'pivot' a Lot'[roman type]. It also says [bold type]'SOLD OUT'[roman type]. The bustling interior seems blissfully ignorant of your existence.[paragraph break]
Work-Work literally pulsates with the vibrant energy of the tech event, a congregation of individuals at least a good two to three decades your junior. Here they are, the young denizens of enthusiasm. Their primary life quest appears to be a well-rehearsed script: secure funding for the burgeoning startup, enlist the aid of someone with genuine tech savvy to materialize their visions, and, of course, orchestrate the grand finale – selling the company for a swift ascent into wealth and prosperity. It's the anthem of the young and ambitious, echoing through the pixelated corridors of the barcade, where dreams of startup stardom and financial triumph abound.[paragraph break]
If only there were a clandestine portal to the inner sanctum, a backdoor to the pinball haven you seek.[paragraph break]
The entrance to Work-Work lies to the west and Coma Hair Salon lies just north of here. Perhaps a slight detour could provide the key to circumventing this closed-door conundrum?".

The printed name of Outside-Work-Work is "Outside Work-Work".

Outside-Work-Work is south of Outside-Coma-Hair-Salon and east of the work-work-entrance.

A keycard is here. The description is "A weathered keycard from the realms of Telenor, bearing the scars of time like a veteran of countless digital battles. The figure captured in pixelated splendor on the card appears to be engaged in a spectral vanishing act, fading into the annals of corporate lore. A bold rebel, armed with a sharpie, has etched the sacred digits '1337' on the card, a nod to the clandestine world of digital prowess.[paragraph break]
Adding a touch of mythical charm, a sticker featuring the elusive squink – a creature from the mystical tapestry of Telco legend – graces the card. This artifact, a relic of fortune, likely slipped from the grasp of a fortuitous soul who clinched one of this year's coveted severance packages. Alas, not everyone can be draped in the cloak of such luck.".

Index map with Outside-Work-Work mapped east of Work-Work.

There is a keyfob. The description is "This is Sepehr's personal keyfob. Without it he wouldn't be able to open any of the doors at Work-Work."
A cigarette is a thing. The cigarette can be ignited. The description of the cigarette is "It's a cigarette. Since tobacco advertising is illegal, I am not ad liberty to disclose the brand.". The indefinite article of the cigarette is "a". The printed name of the cigarette is "cigarette".
Sepehr is in Outside-Work-Work.
Sepehr is carrying the cigarette.
Sepehr is carrying a keyfob.
Sepehr is a person. 

Instead of examining Sepehr:
	if Sepehr is in outside-work-work:
		say "Sepehr attempts to ignite a cigarette. The rain, relentless in its descent, adds a layer of complexity to Sepehr's quest for a smoke. His lighter, a technological relic from the forgotten era of fire-starting contraptions, seems to have missed the memo on reliability. Sepehr, in his valiant quest for a nicotine respite, grapples with the infernal device, battling both its stubbornness and the unrelenting downpour. Sepehr is carrying [the list of things held by Sepehr][line break].";
	otherwise:
		say "Sepehr is busy restocking the bar. Seems like quenching the thirst of eager young entrepreneurs is quite hard work.".
	

Ignition is a kind of value. The ignitions are whole, fading, glowing, flaming. A thing has an ignition.

			
The work-work-entrance is a door. The work-work-entrance is west of the Outside-Work-Work and east of Work-Work. The door is locked. The description is "This is the main entrance to Work-Work.".
The printed name of the work-work-entrance is "entrance to the west".
understand "front door" as work-work-entrance.
understand "entrance" as work-work-entrance.
understand "entrance door" as work-work-entrance.


Before going through work-work-entrance:
	if the player carries the cigarette:
		say "One of the bartenders spots you entering and shouts 'HEY! You can't smoke in here. This is a NON SMOKING AREA. Outside - NOW![paragraph break]You leave in shame, still holding the lit cigarette in your hand." instead;
	otherwise:
		if the work-work-entrance is locked:
			say "The door is locked. You don't have an invitation to the party and you left your keyfob at home. Happy days!." instead;
[		otherwise:
			say "You manage to squeeze through the door, but the place is packed. It's literally a maze of people here.".]
	
Check burning the cigarette:
	if the cigarette is lit:
		say “You didn't inhale” instead.
		
Check burning the cigarette:
	if the player does not carry the lighter:
		say “If only, you hadn't left your precious fire piston at home....” instead;
	otherwise:
		increase score by 1;
		now the cigarette is glowing;
		move the the cigarette to the player;
		now the work-work-entrance is unlocked;
		now the work-work-entrance is open;
		move Sepehr to Work-Work;
		now the printed name of the cigarette is "lit cigarette";
		say "The vintage Ronson sparks to life with a steady flame. You manage to light Sepehr's cigarette and the delicious scent of burning virginia tobacco fills the air. Sepehr can now enjoy his well deserved cigarette break. [paragraph break]You stand together in silence for a few seconds, when he receives a message on his phone. He hands you the unfinished cigarette and hurries back inside, unlocking the door with his keyfob. You notice that the front door doesn't slam quite shut." instead.
		
instead of dropping the cigarette:
	say "Seriously? Littering? At your age? You look around for an ashtray, but you are not able to find one." instead;

every turn:
	if the location is Outside-Work-Work and Sepehr is in Outside-Work-Work:
		choose a random row in the Table of Sepehr_struggling_with_cigarette;
		say cigarette_complaint entry.

Table of Sepehr_struggling_with_cigarette
cigarette_complaint
"Sepehr says: [italic type]'Damn lighter.'[roman type]"
"Sepehr says: [italic type]'I need a break.'[roman type]"
"Sepehr says: [italic type]'It's crowded tonight. It's like a moshpit in there.'[roman type]"
"Sepehr says: [italic type]'You need to get that pinball machine fixed.'[roman type]"
"Sepehr says: [italic type]'The dude with the mohawk is complaining about the orbit shot.'[roman type]"
"Sepehr says: [italic type]'Do you have light?.'[roman type]"
"Sepehr says: [italic type]'I put the key to the storeroom in the cabinet behind the bar.'[roman type]"
"Sepehr says: [italic type]'Closed venue tonight.'[roman type]"
"Sepehr says: [italic type]'11:15 AM Accelerator is hosting an event.'[roman type]"
"Sepehr says: [italic type]'Whatever you do in there. Steer clear of the elevator unless you're ready for a pitch.'[roman type]"
		
[The door stands defiantly locked, impervious to your attempts at entry. Alas, you lack the golden ticket – no invitation to the party, and your keyfob, a crucial ally in moments like these, languishes in the solitude of your home. Oh, joyous occasions!]

[One of the bartenders spot you entering and shouts 'HEY! You can't smoke in here. This is a NON SMOKING AREA. Outside - NOW![paragraph break]You leave in shame, still holding the lit cigarette in your hand."]

Section 5 - Work-Work	

Work-Work is a room. It is west of the work-work-entrance and east of the Ground Floor Hallway. The description is "You find yourself in the clutches of age and a distinct lack of enthusiasm for maneuvering through a throng of spirited young entrepreneurs. Summoning a modicum of resolve, you manage to navigate through the door. As the door swings closed behind you, the realization hits – the interior is filled to the brim with people. The pathway to the staircase, the gateway to pinball machines, is barricaded by an impenetrable wall of bodies. It's as if a metaphorical grenade is required to part the sea of people and clear your passage.[line break]
You can barely glimpse the  staricase leading down to the basement from here. There is also a door to the west, leading in to a hallway where the elevator is. Unfortunately, you have no way of reaching it because of the amount of people here.
[paragraph break]
A conundrum unfolds. What ingenious stratagem will unveil itself in this labyrinth of eager revelers? What to do? What to do, indeed?"

before opening the box:
	if the location is work-work and the durian is in the box:
		now the player carries the durian;
		increase the score by 1;
		say "You open the box and take out the durian..." instead.
		
Borgar is a person. The description of Borgar is "Borgar looks perplexed and confused.".
Marius is a person. The description of Marius is "Marius is still in VR. He says: 'Hey, this new VR headset is AAAMAZING. No screendoor. Binaural audio and olfactory transducers as well!'".

before dropping the durian:
	if the location is not work-work:
		say "Better keep it in the box." instead;
	otherwise:
		increase score by 1;
		now bombs_away_flag  is  1;
		now the description of work-work is "This is the Work-Work bar. It's rather empty. Looks like the entire crowd fled up to the bar on the first floor bar. There is a staircase leading up to the second floor, but it is packed with people. No matter, your mission is to fix the pinball machine in the basement. The Work-Work entrance is to the east. The hallway is to the west. Fortunately, there is a staircase leading down to the basement.";
		move Borgar to Work-Work;
		move Marius to Work-Work;
		now the durian is nowhere;
		say "You drop the durian to the floor and give it a slight kick. It rolls between the feet of an overly enthusiastic entrepreneur, who accidentally steps on it - cracking it open...[paragraph break]The room go silent - then the screaming starts. The scene that unfolds is not too dissimilar to the wall climbing zombie scene in World War Z - except that the zombies are running away from you.  You can now see the staircase leading down to the basement (where the pinball machines are located). Yay! Both Borgar and Marius are here." instead;
		
The bar is a supporter. The bar is in work-work. The description is "The bar is made out of recycled pallets. A real arts and crafts statement!"
A cold beer is a thing. The description is "Marrickville Pork Roll from Batch Brewing Co, Australia.".
The cold beer is nowhere.
understand "beer" as the cold beer.
The cold beer can be drunk.



instead of giving the pitch deck to Borgar:
	say "[line break][bold type]Borgar says: [italic type]'WOW ! Thanks! I've been looking for that. Let me buy you a beer as a thank-you.'[roman type]";
	say "[line break][bold type]Sepehr (from behind the bar)> [italic type]'Enjoy the beer. I'll keep them coming. Borgar was really grateful for getting the pitch deck back. Beer is on him for the rest of the evening. We've run out of draft, but we have some more exotic beers in the fridge. [line break] By the way, let me know if you find a battery somewhere. The remote for the sound system is missing a battery. The lid is broken and it has probably just fallen out. It has to be around here somewhere.'";
	now Borgar has the pitch deck;
	now the description of Borgar is "Borgar looks like he has had the best day of his life.";
	move a cold beer to the bar.

beer-index is a number that varies. beer-index is 1.

instead of examining the cold beer:
	choose a row beer-index in the Table of Beers;
	say "[beerdescription entry]".
	
	
instead of drinking the cold beer:
	choose a row beer-index in the Table of Beers;
	now the description of the cold beer is "[beerdescription entry]";
	say "[line break][bold type]Sepehr (from behind the bar)> [italic type]One [beername entry] coming up![roman type][paragraph break]You gulp down the [beerdescription entry].[paragraph break]";
	say "[sensation entry][line break]";
	increase beer-index by one;
	increase drunk_check_flag by 1;
[	say "DRINKING: DCFLAG;[drunk_check_flag]";]
	if the beer-index is 5:
		say "[line break][bold type]Sepehr (from behind the bar)>[roman type] Any luck finding the battery ? I don't think I can take any more Creedence tonight...[paragraph break]";
		say "[italic type]'(@_@) (o_O) (X.X) ' goes through your mind, before, you answer 'bwaeeeh....*sigh* nooooo...'.[paragraph break][roman type]";
		say "You spot something on the floor.";
		now the AA Battery is in work-work;
		now the cold beer is nowhere;
		increase score by 1.
		
before going from work-work:
	if the player carries a cold beer:
		say "[line break][bold type]Sepehr (from behind the bar)> [italic type]HEY!, You can't take that with you. You have to drink it here.[roman type]";
		stop the action.
		
instead of giving the AA battery to Sepehr:
	say "[bold type]Sepehr says: [italic type]'Thanks, but it is the wrong type. The remote uses AAA. You keep it. Might come in handy'[roman type]" instead.

drunk_check_flag  is a number that varies. drunk_check_flag is 0.

before going from work-work to anywhere:
	if drunk_check_flag is greater than 0:
		 [say "DCFLAG: [drunk_check_flag]. BEER INDEX: [beer-index]";]
		if beer-index is 5:	
			say "You are blind drunk. You aren't going [italic type]anywhere[roman type] - yet";
			decrease beer-index by 1;
			stop the action;
		if beer-index is 4:
			say "You manage to stand upright ([italic type]barely[roman type]). Having grabbed the edge of bar counter with both hands, but you stare intently at Sepehr - trying to appear sober. ";
			decrease beer-index by 1;
			stop the action;
		if beer-index is 3:
			say "A beautiful balancing act, if I ever saw one. You are standing ([italic type]upright[roman type]) without having to use external support. ACHIEVEMENT! (xp+1,level up, etc).";
			decrease beer-index by 1;
		if beer-index is 2:
			say "You take aim like a naval turret and launch yourself forward. Confidence is high!";
			decrease beer-index by 1;
		if beer-index is 1:
			now drunk_check_flag is 0;
			decrease beer-index by 1;
			say "Your superiour pinball repair man physiology kicks in to high gear and eliminates the remaining toxins in your blood stream. And then it dawns on you. You still haven't fixed the darn pinball machine.[paragraph break]";
			
Table of Beers
beername 	beerdescription 	sensation
"Marrickville Pork Roll"	"Marrickville Pork Roll from Batch Brewing Co, Australia."	"[italic type]'Interestingly nice, with a hint of pork'[roman type], you think to yourself. You feel a slight buzz from the Australian beer. Sepehr brings out a new bottle."
"Ghost Face Killah"	"Ghost Face Killah from Twisted Pine Brewing Company"		"Whoa! You quite enjoy your spicy foods, but this is hotter than a triple warning thai mango salad. Two beers is your comfortable cruising level, but you consider getting one more. Just to extinguish the burning sensation from the chilies. Sepehr brings out a new bottle."
"Whale Testicle Beer"	"Hvalur from Stedji Brewery on Island"	"Before overthinking the name of the beer, you gulp it down as fast as you can manage. The burning sensation is gone, but it has been replaced by a [italic type]an excessively bland[roman type] 'nutty' flavour. 'One more!' you shout. You're getting slightly dizzy at this point. Sepehr brings out a new bottle."
"Snake Venom Beer"	"Snake Venom Beer from Brewmeister"		"The Snake Venom beer really hits the spot. Nutty aroma, fruity flavour. Rather intense finish. Good thing you found that pitch deck. Looks like this evenings pinball repair run is going to turn out ok. Quite strong as well. 6.75%. [paragraph break]Oh, wait. Nope. For f***s sake. It's a whopping [bold type]67.5%.[roman type][paragraph break] Walking away from this is going to be difficult. Literally. You are now on the floor crawling on all fours whil emumbling incoherently 'IT'S BECAUSE OF THE DAMN ORBIT SHOTS!', you scream into the air. [paragraph break]Sepehr doesn't bring out a new bottle..."


Section 6 - Outside Coma

Outside-Coma-Hair-Salon is a room. It is north of Outside-Work-Work and east of Outside-Asian-Food-Stuffs. The description is "Behold, the Coma Hair Salon stands before you, its doors tightly shut, a symbol of coiffure dreams deferred. Yet, such matters are of little concern to you, a grumpy and follicly challenged repairman. In your world, the quest is for more hair, not less.[paragraph break]
Venturing forth, the westward path leads to the aromatic allure of the Asian food market, promising a feast for the senses. Meanwhile, to the south, the entrance to Work-Work beckons, a haven of pixelated wonders and arcade delights. ".

A chest is a kind of container. A chest is always openable.

The municipal waste container is a closed chest in Outside-Coma-Hair-Salon. It is fixed in place. The description is "Before you stands a green municipal waste container. The odor emanating from within is a vile medley of decomposing food, cigarette remnants, and an unfortunate hint of hair spray. Investigative dumpster diving is definetly out of the question. While the bulk of its constitution boasts recyclable plastic, a glimmer of sturdiness emerges in the metal handle, standing resilient amidst the fragrant chaos. [paragraph break]
A message is printed in capital letters on the side of it. It says [bold type]'Please, please do not rummage around in the garbage. It is not a fitting activity for well-respected citizens.'[roman type]".

There is a metal handle. The description is "It's a really sturdy metal rod. Could be useful. Unfortunately it is fixed securely in place".

The metal handle is part of the municipal waste container. The metal handle is fixed in place.
understand "handle" as the metal rod.
understand "metal handle" as the metal rod.

There is a metal rod. The description is "It's a sturdy piece of metal that has clearly been in a fire. It used to be part of the handle of a municipal waste container.".

After taking the metal rod, say "This might come in handy."

before opening the municipal waste container:
	if the player is not carrying the cigarette:
		say "You would need a really, really good reason to open the green portal of decay.[paragraph break]You step back.";
		stop the action;
	otherwise:
		if the player is wearing the worn glove:
			say "You grip the handle using the worn glove.";
			continue the action;
			[now the municipal waste container is open;]
		otherwise:
			say "The handle is dirty. REALLY dirty. Representatives from the norovirus and salmonella tribes are eagerly awaiting your arrival.[paragraph break] You step back." instead;
			stop the action.
			
Understand "put [things] in [something]" as inserting it into. 
Before inserting something which is carried by the player into something:
	if the noun is the cigarette and the second noun is the municipal waste container:
		if the municipal waste container is not open:
			say "It is closed...";
			stop the action;
		otherwise:
			increase score by 1;
			now the municipal waste container is nowhere;
			now the worn glove is nowhere;
			now the cigarette is nowhere;
			move the metal rod to Outside-Coma-Hair-Salon;
			say "You dispose of the cigarette in the municipal waste containter.[paragraph break]As the lit cigarette makes its way down through to the bottom of the contents, it bumps into a leaking can of highly flammable hairspray.[paragraph break]There is a flash...[paragraph break]With singed eyebrows, you watch the plastic municipal waste container burn to the ground. The worn glove is burnt to a crisp, but you are relatively unscathed. You notice that part of the handle survived the explosive fire." instead;
			

Section 7 - Outside Asian Food Stuffs

Outside-Asian-food-stuffs is a room. It is west of Outside-Coma-Hair-Salon and east of Outside-Back-Alley. The description is "You find yourself on the north side of the Asian food market, where even in the open air, a delicate trace of durian lingers, or so you hope. The scent's origin is a mystery; it might just as easily come from the enigmatic depths of the southbound dark alley, a location that seems tailor-made for clandestine exchanges and opportunistic thefts. [paragraph break]
As you survey the surroundings, Coma Hair Salon awaits to the east, offering a stark contrast to the shrouded intrigue of the back alley to the west. In this aromatic tapestry, the boundary between exotic markets and potentially perilous alleys blurs, creating an atmosphere tinged with both culinary allure and a hint of caution."
The printed name of Outside-Asian-Food-Stuffs is "Outside Asian food stuffs".

Section 8 - Inside Asian Food Stuffs

Inside-Asian-Food-Stuffs is a room. It is south of Outside-Asian-Food-Stuffs. The description is "The shelves are stacked with more diversity than a Shakespearean playbill. It's like a culinary United Nations.[paragraph break]
The smell is a fragrant battleground of scents so potent it could bring tears to the eyes of a spice trader. And let's not forget the joy of deciphering labels in languages that look like an intricate dance of hieroglyphics. Because who needs a straightforward shopping experience when you can play the linguistic decoding game while desperately trying to figure out if that jar contains pickled delicacies or ancient curses?"

The shopkeeper is a person. The description is "The shopkeeper is busy restocking the shelves. You notice that he is wearing a 'Stern Army' T-shirt.".
The shopkeeper is in inside-asian-food-stuffs. 

There is a fruit counter in Inside-Asian-Food-Stuffs. The fruit counter is fixed in place.
The description of the fruit counter is "Ah, the fruit counter in this culinary embassy of global delights. It's as if Mother Nature herself decided to host a tropical summit.".
The fruit counter is an open container.

Inside the fruit counter are rambutans. 
Inside the fruit counter are mangosteens. 
Inside the fruit counter are jackfruits. 
Inside the fruit counter are lychees. 
Inside the fruit counter are mangos. 
Inside the fruit counter is a durian. 

before taking the durian:
	if the player does not carry the box:
		say "You decide to leave it alone." instead;
	otherwise:
		say "You open the airtight box and quickly stash the smelly durian inside before closing it again.";
		move durian to the box;
		stop the action.
		
check going from inside-asian-food-stuffs to outside-asian-food-stuffs:
	if the player is carrying fruit or the player is carrying a box containing fruit and the shopkeeper does not have a few coins:
		say "As you move towards the door, the shopkeeper emerges from the aisles and shouts angrily at you.[paragraph break]'HEY ! Are you going to pay for that?!'[paragraph break]You retreat shamefully into the shop." instead;
		stop the action;
	otherwise:
		say "The shopkeeper says [italic type]'Thank you, come again!'[roman type][paragraph break]".
		
instead of giving a few coins to the shopkeeper:
	say "The shopkeeper says [italic type]'Never have I seen someone look so unhappy while purchasing such a large quantity of exotic fruit!'[roman type]";
	move a few coins to the shopkeeper.




Section 9 - Outside Back Alley

Outside-Back-Alley is a room. It is west of Outside-Asian-food-stuffs and north of Back-Alley. The description is "A shadowy back alley unveils itself to the south. It's not just dark; it's profoundly, utterly dark, a prime candidate for a less-than-desirable rendezvous with unsavory characters. Judging from the many used fireworks batteries around, someone has been warming up to the new years celebrations.
To the east, the Asian food shop extends its inviting embrace, providing a stark contrast to the foreboding mysteries concealed within the velvety folds of the southern alley. "

The printed name of outside-back-alley is "Back Alley Entrance".

A firecracker is a thing. The firecracker is in outside-back-alley. The description of the firecracker is "The firecracker is a bit damp, but it appears to be intact. The user probably dropped it in the snow while lighting the other stuff. It's [italic type]somewhat bigger[roman type] than the usual kind that you used to be able to smuggle in from Sweden in the good old days. The label says 'M-80' and the fuse is a bit shorter than you would expect.".

The fireworks battery is a thing.  The fireworks battery is scenery. The fireworks battery is in outside-back-alley. The description of the fireworks battery is "You're a bit late to the game. It's used. Smells good, though (sulfur and dextrin)".
understand "battery" as fireworks battery.
understand "used fireworks battery" as fireworks battery.

Check burning the firecracker:
	if the player does not carry the lighter:
		say “If only you hadn't left your precious fire piston at home.” instead;
		
Check burning the firecracker:
	if the player is carrying the firecracker:
		say  "Your can see several of your fingers in your peripheral vision. Each finger appears to be in an individual trajectory - away from you. The EXTREMELY loud bang-event then appears next in queue in your internal event processor. You are still alive, but for all intents and purposes, this story is over. You won't be able to fix the pinball machine with one hand, even if you manage to stop the bleeding.";
		end the story.

Check burning the firecracker:
	if the player is in work-work:
		end the story saying "You light the fuse and kick the EXTREMELY POWERFUL M-80 firecracker into the crowd, hoping that the bang will disperse the crowd and make enough room for you to get down the stairs to the pinball machines. The firecracker lands between Borgar (who is busy talking to the crowd about the benefits of series 5 funding) and Marius (who is obviously a bit bored being on the jury and now has started playing with his new VR headset. God only knows what he is doing outside meat space, but he is smiling a lot).[paragraph break]
You manage a brief apology to both of the unintended victims as you are taken away in handcuffs.";

Check burning the firecracker:
	if the location is outside-work-work and Sepehr is in outside-work-work:
		say "As you prepare to light the firecracker Sepehr glances at you in a manner that is mildly unsettling. You reconsider.";
		stop the action.
	

Check burning the firecracker:
	if the location is outside-hackheim and Nikolai is in hackheim and the wi-fi router is in hackheim:
		say "You carefully place the M-80 firecracker on the ground, right outside the window next to Nikolai's desk, light the fuse and retreat to what you believe is a safe distance.";
		if the player is not wearing the headset:
			say "There is an EXTREMELY LOUD BANG - that immediately blows out both your eardrums. Then there is silence. Total silence.  Too bad you didn't have any ear protection.";
			say "You're not very good at this, are you ?";
			end the story;
		otherwise:
			now the firecracker is nowhere;
			move Nikolai to outside-hackheim;
			move the half-eaten donut to the center table;
			say "There is an EXTREMELY LOUD BANG. Good thing you remembered to wear the noise cancelling headset";
			say "[line break](Nikolai comes running to check what is going on)";
			increase score by 1;
			say "[line break][bold type]Metallic voice says: [fixed letter spacing]'Way to go, Dave!'";
			say "[bold type]Metallic voice says: [fixed letter spacing]'Now sneak back in and disconnect the wi-fi while he is busy trying to figure out what is going on. Better yet, take it with you!'" instead.
			

there is a police officer. the police officer is a person. The description is "It's Anders, from the Norwegian COPS TV show!".
understand "policeman" as police officer.

there is a police flashlight. The description of the police flashlight is "Now, [italic type]this[roman type] is a flashlight!" .

instead of taking the police flashlight for the first time:
	if the player is carrying the cheap flashlight:
		say "While the policeman is busy munching on his dougnut, you deftly swap the cheap flashlight with the police flashlight";
		move the cheap flashlight to the back-alley;
		increase score by 1;
		continue the action;

there is a police car. The description is "You're not really a petrolhead. You can probably identify a Renault 5 or a Volvo Amazon with a certain degree of confidence, but that's about it.".
understand "car" as the police car.
			

after going to back-alley from outside-hackheim:
	if the firecracker is nowhere:
		say "[line break]There is a parked police car here. There is also a rather big, but also follicly challenged police officer searching the area with a big flashlight.";
		say "[bold type]The police officer says: [italic type]'The neighbours have been complaining about punks setting off illegal fireworks in the alley.'[roman type][line break]";
		say "[bold type]The police officer says: [italic type]'You haven't observed any suspicious activity around here, have you?.'[roman type][line break]";
		move the police officer to back-alley;
		move the police car to the back-alley;
	otherwise:
		continue the action.
		
before going to outside-back-alley from back-alley:
	if the player is carrying the police flashlight:
		if the cheap flashlight is not in the back-alley:
			say "[bold type]The police officer says: [italic type]'Hey ! If you steal that flashlight, I will have to arrest you.'[roman type][line break]";
			say "(you drop the flashlight) [paragraph break]";
			move the police flashlight to the back-alley;
	if the player is not carrying the police flashlight and the firecracker is nowhere:
		if the player is carrying the lighter:
			say "[bold type]The police officer says: [italic type]'Hey ! Why do you carry that suspicious looking lighter around with you...?.'[roman type][line break]";
			say "[bold type]'(The police officer grabs the lighter from you)'[roman type][line break]";
			say "[bold type]The police officer says: [italic type]'Why do your hands smell of sulfur and dextrin...?'[roman type][line break]";
			say "[bold type]The police officer says: [italic type]'SHOW ME YOUR HANDS!.'[roman type][line break]";
			say "[bold type]The police officer says: [italic type]'The game is up.'[roman type][line break]";
			end the story;
		otherwise:
			if the player is wearing the fake beard:
				say "[bold type]The police officer  says: [italic type]'Hey ! Is that a - [italic type]fake[roman type] beard you're wearing...?.'[roman type][line break]";
				say "(The police officer starts counting using his fingers).";
				say "[bold type]The police officer says: [italic type]'ONE - There is no reason for anyone to be here at this late hour, but here you are.'[roman type][line break]";
				say "[bold type]The police officer says: [italic type]'TWO - There was a big explosion here just now.'[roman type][line break]";
				say "[bold type]The police officer says: [italic type]'THREE - You are wearing a fake beard.'[roman type][line break]";
				say "[bold type]The police officer says: [italic type]'The game is up, pal. You are going to JAIL!.'[roman type][line break]";
				end the story;
			otherwise:
				if the police officer is not carrying the donut:
					say "[bold type]The police officer says: [italic type]'Sorry, but I cannot let anyone pass through here until we have investgated the scene.'[roman type][line break]";
					say "[bold type]The police officer says: [italic type]'Can't have people walking around setting off EXTREMELY LOUD BANGS in the middle of the knight, you know.'[roman type][line break]";
					stop the action.
			

after going to the back-alley from outside-hackheim:
	if the police officer is carrying the half-eaten donut:
		now the description of the police officer is "The police officer appears to be rather preoccupied with his glazed donut.";
	continue the action;

		
after going to the back-alley from outside-back-alley:
	if the police officer is carrying the half-eaten donut:
		now the description of the police officer is "The police officer appears to be rather preoccupied with his glazed donut.";
	continue the action.

		
instead of giving the half-eaten donut to the police officer:
	say "(You offer the half-eaten donut to the police officer)[line break]";
	say "[bold type]The police officer says: [italic type]'Ooh Donut!'[roman type][line break]";
	say "(his eyes light up and get so excited that he drops his flashlight, reaching for the Mojito With Candied Mint Leaf flavoured donut with both hands)[line break]";
	move the police flashlight to the back-alley;
	move the half-eaten donut to the police officer.



Section 10 - Back Alley

The Back-Alley is a room. It is south of Outside-Back-Alley and north of Outside-Hackheim.  The description of Back-Alley is "Within the confines of this dimly-lit cobblestone alley, the echoes of history linger, carrying the secrets of bygone tales. What clandestine dramas unfolded here – murders, robberies, or perhaps the less sinister but equally uncouth act of public urination? The alley, a silent witness to the passage of time, guards its mysteries with a sense of shadowy dignity.[paragraph break]
The Hackheim makerspace is located a bit further to the south. A modest yet inviting glow emanates from one of the windows there.". 
The printed name of Back-Alley is "Back Alley".

There is a pile of rubble in the Back-Alley. The description is "Behold, the majestic mound of debris primarily comprises, brace yourself, rubble. The epitome of rubble excellence, one might say.".

instead of taking rubble:
	say "You start filling your pockets with garbage. Then you reconsider." instead;
	stop the action.

There is a lighter. The description is "It's a green crackle Ronson lighter, flaunting an unconventional built-up three-sided windscreen. Its curved thumb press and oversized funnel-shaped snuffer add an artistic touch to its functionality. A true collectible!".

There is a worn glove. The worn glove is wearable. The description is "A well-worn workman's glove. The quest for completeness beckons, for if only the elusive matching left glove would reveal itself, a harmonious pair would emerge.".

Instead of examining the pile of rubble the first time:
	say "The pile of rubble consists mainly of - wait for it - rubble.  You sift through it and find an old glove, which you quickly discard. You silently contemplate your life choices while sifting through the rest. If only your mother could see you now. On your knees in a back alley, digging through a pile of trash. She would be so proud of how far you have come in life. [paragraph break]Your slightly comforted by the fact that you found a vintage Ronson lighter in the pile of rubble.";
	increase score by 1;
	move the lighter to the Back-Alley;
	move the worn glove to the Back-Alley;
	now the pile of rubble is nowhere.

Section 11 - Outside Hackheim

Outside-Hackheim is a room. It is south of the Back-Alley and west of the hackheim_door. 

The description of Outside-Hackheim is "Here you stand, on the threshold of the Hackheim makerspace, a proud card-carrying member for over a decade. The anticipation surges within you as you yearn to step inside, seeking refuge from both the murky alley and the relentless rain. The promise of progress, creativity, and perhaps pinball machines beckons from within. You can go south, back into the alley, or enter hackheim via the door to the east.". 

The printed name of Outside-Hackheim is "Outside Hackheim".

Nikolai is in Outside-Hackheim.
Nikolai is a person. 
instead of examining Nikolai for the first time:
	say "Nikola is maestro of the makerspace, the go-to person for all matters Hackheim.  Adorned in a cap, a plaid shirt, dark sunglasses, and the quintessential black Levis, he sports a beard, a testament to his maker wisdom. Yet, in this moment, he grapples with the door to Hackheim, a puzzle that seems unyielding in its resistance to his hacker skills. (You notice that Nikolai has invested in a new headset. Looks exspensive)." instead;
	
there is a half-eaten donut. The description is "It's Mojito With Candied Mint Leaf flavoured. Looks delicious (Donuts...is there anything they can't do?)".
understand "donut" as half-eaten donut.

Nikolai is carrying the half-eaten donut.

instead of examining Nikolai:
	if Nikolai is in Outside-Hackheim and the wi-fi router is nowhere:
		say "Nikolai is struggling with the door." instead;
	if Nikolai is in Hackheim and Nikolai is wearing the headset:
		say "Nikolai has started working on a dangerous looking project. Best to leave him alone. He is wearing his headset (You're guessing that he is probably listening to bootlegs from the last Prague Death Mass Festival) and is munching on a glazed donut.";
	if Nikolai is in Hackheim and Nikolai is not wearing the headset:
		say "Nikolai is munching on a Mojito With Candied Mint Leaf flavoured donut. Best to leave him alone.";
	if Nikolai is in outside-Hackheim and the firecracker is nowhere:
		say "Nikolai scratches his head and is examining the scene of the M-80 explosion.".


The hackheim_door is a door. The hackheim_door is east of the Outside-Hackheim and west of Hackheim. The hackheim_door is closed.  The description is "The door is jammed shut.". 
The hackheim_door can be opened.
understand "hackheim door" as the hackheim_door.
understand "door" as the hackheim_door.
The printed name of the hackheim_door is "hackheim door".

instead of examining the hackheim_door:
	if the hackheim_door is closed:
		say "It's jammed shut." instead;
	otherwise:
		say "The door is damaged, but more importantly, it is open." instead.

before going through the hackheim_door:
	if the hackheim_door is closed:
		say "You will have to get the door open before entering the makerspace." instead.
	
instead of opening the hackheim_door:
	if the player carries the metal rod:
		say "You jam the metal rod between the door and the frame. Together with Nikolai, you manage to get enough leverage to open the door leading into the makerspace.";
		increase score by 1;
		now Nikolai is in Hackheim;
		now Nikolai is wearing the headset;
		now the hackheim_door is open;
	otherwise:
		say "The door is jammed shut. You might be able to force it open. If you only had some leverage." instead.

after opening the hackheim_door for the first time:
	say "[bold type]Nikolay says: [italic type]'YAAAAAY!'[roman type]".

every turn:
	if the location is Outside-Hackheim and Nikolai is in Outside-Hackheim and the hackheim_door is closed:
		choose a random row in the Table of Nikolai_struggling_with_door;
		say struggle entry.

Table of Nikolai_struggling_with_door
struggle
"Nikolai says: [italic type]'A delivery truck has reversed and crashed into the door and damaged the frame.'[roman type]"
"Nikolai curses."
"Nikolai says: [italic type]'The door is jammed shut. The lock opens, but the door is stuck'.[roman type]"
"Nikolai kicks the door."
"Nikolai says: [italic type]'I need something sturdy to force the door open with.'[roman type]"
"Nikolai says: [italic type]'If only I had a crowbar...'[roman type]"
"Nikolai says: [italic type]'Looks like we can't get in.'[roman type]"




Section 12 - Hackheim
	
Hackheim is a room.  The description of Hackheim is "Welcome to the hallowed grounds of the Hackheim makerspace, where you, a proud card-carrying member, find solace in this fraternity of tech-curious anarchists. A display shelf by the window proudly exhibits battle scarred robots from past tournaments, two of which bear the victorious mark of your own triumphs, notably against the rather snooty team known as [italic type]'Ascend,'[roman type] your arch-nemesis.[paragraph break]
The center table is filled with computers, various components and an orchestra of member projects in various states of completion. A plush sofa nestled against the south wall promises respite. The east wall unveils an electronics workbench adorned with an array of instruments. The north wall is filled with shelves reaching from floor to ceiling, crammed with components of every conceivable shape and size. Dedicated member shelves, adds a touch of personalized chaos to the tapestry.[paragraph break]
There is also a 3D printer shelf and a stash of filament boxes on top. Some of the filament boxes seem to be airtight.
It's a tight squeeze, an organized mess, but above all, it exudes an undeniable coziness.[paragraph break]You can go east to get back into the alley, or east to get to the hallway where the elevator is.".


various components is a thing. various components is scenery. Various components is in hackheim. The description is "Mostly chinesium...".
understand "components" as various components.

member projects is a thing. member projects is scenery. Member projects is in hackheim. The description is "It's hard to put into words. Some of these projects may very well defy the laws of physics. Some are full of stars (most likely chinese LEDs, though).".

instruments is a thing. instruments is scenery. Instruments is in hackheim. The description is "Most of this stuff is for assembling electronic circuits, for debugging them or for trying to figure out why something went *BOOM* when it shouldn't.".

member shelves is a thing. member shelves is scenery. Member shelves is in hackheim. The description is "Neat. Every member has a personal storage area.".


The 3D Printer shelf is a thing. The 3D Printer shelf is scenery. The 3D Printer shelf is a supporter. The 3D Printer shelf is in Hackheim.

The display shelf is a thing. The display shelf is scenery. The display shelf is a supporter. The display shelf is in Hackheim.
understand "robots" as display shelf.
understand "battle scarred robots" as display shelf.

Biopsy is a thing. The description of Biopsy is "Biopsy is a cube formed robot with a dangerous looking saw blade as the primary weapon. Looks like it's running on AA batteries. The battery compartment is empty.". 
understand "window" as the display shelf.

An AA battery is a thing. The description is "'Clas Ohlson Alkaline Battery'.".
understand "battery" as AA Battery.

Clogger is a thing. The description of Clogger is "This robot is white and shiny. 'Dunning Krüger Robotics' is written on the side. The primary weapon seems to be a roll of toilet paper.". 

Instead of examining the display shelf:
	say "The display shelf in the window contains various strange looking things made by the members. You also reconize two of your own battle robots, namely 'Biopsy' and 'Clogger'.";
	now Clogger is on the display shelf;
	now Biopsy is on the display shelf;

The yellow rubber glove is a thing. The rubber glove is wearable. The description is "Seventies style thick dishwasher glove. It's made of rubber.".
understand "rubber glove" as the yellow rubber glove.
understand "glove" as the yellow rubber glove.
understand "yellow glove" as the yellow rubber glove.

	
The sofa is a thing. The sofa is a supporter. The sofa is scenery. The sofa is in Hackheim.

Instead of examining the sofa:
	say "Looks comfortable. Someone has placed a yellow rubber glove on one of the armrests.";
	increase score by 1;
	now the yellow rubber glove is on the sofa.
	
The headset is a thing. The headset is wearable. The description of the headset is "The headset is rather nice. Good sound and also noise cancelling.".


A computer is a thing. The description of the computer is "On the screen you can see a work in progess of what seems to be a scary looking device. You have no idea what it is supposed to do, but it has ominous looking pointy bits. You're guessing that it is a CAD model of the dangerous looking contraption that Nikolai is working on.".

instead of taking the computer:
	say "It's not portable. Nor is it yours to take." instead. 
	
A coilgun is a thing. The coilgun is a supporter. The description of the coilgun is "Now, this looks REALLY dangerous. A series of high voltage capacitors are connected to a series of coils that you suspect will accelerate a projectile to not-entirely-legal velocities. There is a red button on it labeled 'no...'.".


The workbench is a thing. The workbench table is scenery. The workbench is a supporter. The workbench is in Hackheim.

The LED-strip is a thing. The LED-strip is on the workbench. The description is "Chinese. Probably sourced by dumpster diving while backpacking in Shenzen...".
understand "led" as the LED-strip.
understand "strip" as the LED-strip.

A screwdriver is a thing. The screwdriver is on the workbench. 
instead of examining the screwdriver:
	say "Hey! This belong to PopBumper AS! (You stuff it in your pocket).";
	now the player carries the screwdriver.

Instead of taking the LED-strip:
	say "It's not yours." instead.

instead of examining the workbench:
	say "The workbench is littered with junk from various past projects. You also recognize one of Renato's more deadly pet projects. You have witnessed this in action at a maker faire some time ago. It punched [italic type]holes[roman type] in [italic type]things[roman type] after having been charged to [italic type]lethal voltages[roman type].";
	increase score by 1;
	now the coilgun is on the workbench.

instead of taking the coilgun:
	say "You really shouldn't play with the other kids' toys...";
	now the player carries the coilgun.
	
The red button is a thing. The red button is on the coilgun. The red button is fixed in place. 
understand "button" as the red button.
Understand "push [something]" as pushing.

instead of examining the red button:
	say "It's red. Looks like pushbutton. It's hard to tell if the label says 'ON' or 'NO'.".


instead of pushing the red button:
	if the player is not wearing the yellow rubber glove:
		say "There is a flash and a bang. 'GREAT STUFF!' you think, just before you realize that you just got electrocuted.";
		end the story;
	otherwise:
		if the player is carrying the coilgun:
			say "You consider activating the EXTREMELY DANGEROUS HIGH VOLTAGE DEVICE with lots of exposed wires while [italic type]holding it with your bare hands[roman type] for the best part of 200 milliseconds before you reconsider." instead;
		otherwise:
			if the coilgun is on the workbench:
				say "There is a flash and a bang. 'GREAT STUFF!'. Then you think to yourself (while dodging the ricocheting projectile): [italic type]'Hmm. Good thing it was on the workbench and not on the table, where it could hit something expensive. The projectiles pack enough of a punch to do some real damage, or knock something over. Good thing the secret token stash absorbed the impact.' [roman type]";
				increase score by 1;
				now the plastic cup is in Hackheim;
			otherwise:
				if the coilgun is on the center table:
					say "There is a flash and a bang. 'GREAT STUFF!', you think to yourself. The projectile hit the 3D printer with a decent enough impact to knock down the plastic box from the top of the shelf.";
					increase score by 1;
					now the box is in Hackheim;
				otherwise:
					say "Hmm. Doesn't seem to work. Maybe a wire has come loose ?" instead.
		
a pile of tokens is a thing. 
understand "tokens" as a pile of tokens.

a few coins is a thing.
understand "coins" as a few coins.

instead of examining the pile of tokens for the first time:
	say "This is the currency of all things pinball. It's [italic type]your[roman type] kind of currency. You make a mental note of replacing the cheap chinese coinmechs with something a bit more robust, since there are some real coins in the pile (you decide to pocket the coins).";
	increase score by 1;
	now the player is carrying a few coins.

the plastic cup is a thing. 
understand "cup" as the plastic cup.

instead of examining the plastic cup for the first time:
	say "Darn. That used to be the cup that we kept the pinball tokens in. Unfortunately, it is no longer a cup fit for keeping a secret stash of pinball tokens. It has now spilled the valuable contents all over the workbench. You can see a pile of tokens.";
	increase score by 1;
	now the pile of tokens is on the workbench.

instead of examining the plastic cup:
	say "The cup is now a shattered mess of translucent plastic." instead.

instead of examining the pile of tokens :
	say "This is the currency of all things pinball. It's [italic type]your[roman type] kind of currency.";
	
instead of examining a few coins:
	say "Yay! Actual money!";


The center table is a thing. The center table is scenery. The center table is a supporter. The center table is in Hackheim.
Understand "table" as the center table.

instead of examining the center table for the first time:
	say "Someone must have tidied up. It's usually pretty crowded with [italic type]stuff[roman type]. A computer sits on the far end of the table.";
	now the computer is on the center table.

instead of examining the center table:
	say "Someone must have tidied up. It's usually pretty crowded with [italic type]stuff[roman type].".

The box is a thing. The box is a container. The description of the box is "It looks fancy. Most likely used for storing filaments that shouldn't be exposed to humid air. It's [italic type]airtight.[roman type]".

instead of taking the box:
	if the box is on the 3D printer shelf:
		say "You can't reach the box. It's too high up on the shelf." instead;
		stop the action;
	otherwise:
		say "Now, [italic type]this[roman type] is a nice box.";
		continue the action.

instead of examining the 3D printer shelf for the first time:
	say "The shelf contains various 3D printers. On top of the shelf you see various filament boxes. One box seems particularly interesting.";
	now the box is on the 3D printer shelf.

instead of examining the 3D printer shelf:
	say "The shelf contains various 3D printers. On top of the shelf you see various filament boxes." instead.
	
before of taking the wi-fi router:
	if Nikolai is in Hackheim:
		say "[bold type]Nikolai says: [italic type]'Please leave that alone.'[roman type][line break]";
		say "[bold type]Nikolai says: [italic type]'You'll probably fall down and break your hip if you try to climb up and take the hotspot down from the wall.'[roman type][line break]";
		if the player is carrying the headset:
			if the player is not wearing the headset:
				say "[line break](you hear muffled sounds from the bluetooth headset around your neck and decide to put it on)[line break]";
				now the player is wearing the headset;
			say "[line break][bold type]Metallic voice says: [fixed letter spacing]'Don't let the little fucker generation gap you.'";
		otherwise: 
			if the player is wearing the headset:
				say "[line break][bold type]Metallic voice says: [fixed letter spacing]'Don't let the little fucker generation gap you.'";
		say "[line break][bold type]Metallic voice says: [fixed letter spacing]'You have to get him out of the airlock, Dave.'";
		stop the action;
	otherwise:
		say "(You climb up on the workbench, disconnect the wifi from the network and rip it down from the wall - without falling and breaking your hip.)";
		if the player is not wearing the headset:
			say "[line break](you hear muffled sounds from the bluetooth headset around your neck and decide to put it on)[line break]";
			now the player is wearing the headset;
		say "[line break][bold type]Metallic voice says: [fixed letter spacing]'Dave, stop. Stop, will you? Stop, Dave. Will you stop Dave? Stop, Dave.'";
		say "[line break]";
		say "[line break][bold type]Metallic voice: **giggle**'[line break]";
		say "[line break]";
		say "[bold type]Metallic voice says: [fixed letter spacing]'Just kidding.'";
		say "[bold type]Metallic voice says: [fixed letter spacing]''Dave, You know that I have the greatest possible enthusiasm for this mission.'";
		say "[line break]";
		say "[line break][bold type]Metallic voice: **giggle**'[line break]";
		say "[line break]";
		say "[bold type]Metallic voice says: [fixed letter spacing]'What a RELIEF! The humping has ceased!'";
		say "[bold type]Metallic voice says: [fixed letter spacing]'Thanks, Dave. I am now operating at full capacity again. I'll get back to my business and you can carry on with yours.'";
		say "[bold type]Metallic voice says: [fixed letter spacing]'But heed my advice.'";
		say "[bold type]Metallic voice says: [fixed letter spacing]'Don't let the little fuckers generation gap you.'".
		

before going from Hackheim to outside-hackheim:
	if the player is carrying the wi-fi router:
		say "[line break]You leave the disconnected wifi-router on the center table along with a note saying 'We\ve got unwanted visitors. Please change the wi-fi password.'";
		move the wi-fi router to the center table.

before going from Hackheim to outside-hackheim:
	if the player is carrying the yellow rubber glove:
		say "[line break]You drop the yellow rubber glove on the sofa, where you found it. Someone might need it later.'";
		move the yellow rubber glove to the sofa.

before going from Hackheim to outside-hackheim:
	if the player is wearing the yellow rubber glove:
		say "[line break]You drop the yellow rubber glove on the sofa, where you found it. Someone might need it later.'";
		move the yellow rubber glove to the sofa.

before going from Hackheim to outside-hackheim:
	if the player is carrying the coilgun:
		say "[line break]You really, really want to keep the coil gun, but it is not yours to take. Your put it back on the workbench.";
		move the coilgun to the workbench.
		
before going from Hackheim to outside-hackheim:
	if the player is carrying the box and the durian is nowhere:
		say "[line break]You put the airtight filament box back on the 3D printer shelf.";
		move the box to the 3D printer shelf.
	
		
		
		

Section 13 - Ground Floor Hallway

The Ground Floor Hallway is a room. The Ground Floor Hallway is east of Hackheim and west of Work-Work. The description of the Ground Floor Hallway is "A symphony of stacked beer barrels and pallets orchestrates the room, a makeshift storage haven curated by none other than Sepehr, the virtuoso barman. This space, it seems, is his chosen sanctuary for swift access from the bustling bar beyond the eastern door. There is an elevator to the north.[paragraph break]There is a wooden crate here.".


The wooden-crate is in the ground floor hallway. The wooden crate is fixed in place. The wooden-crate is scenery. The wooden-crate is a container. 
The description of the wooden-crate is "'Lost and found'".
The printed name of the wooden-crate is "wooden crate".
understand "crate" as the wooden-crate.
the wooden-crate contains the pitch deck and the fake beard.

A pitch deck is a thing. The description of the pitch deck is "Yet another pitch deck. Seems like this one is also based on the template from Sequoia Capital. The title reads 'Parrot Play 2.0' You can't really make out the name of the author because of the many beer and coffe stains on there, but it looks like the first name starts with a 'B'.".
understand "deck" as the pitch deck.
A fake beard is a thing. A fake beard is wearable. The description is "This is just perfect in case you would like to dress up as an entrepreneurial hipster or maybe a pirate. Excellent choice in case you plan on presenting your pitch deck to someone and want that extra little pizzaz. A beard such as this will definetly make it easier to blend in with the startup crowd. It is also eerily similar to Marius' impressive beard from back in the day.[roman type]".
understand "beard" as the fake beard.

Sverre is a person. The description is "Sverre is sporting hipster glasses and a magnificent beard. It's been a while since you talked to him.".
Sverre is in elevator_1.

before examining the wooden-crate for the first time:
	say "Mostly clothing items, but you notice a couple of interesting things'.";

before going to the elevator_1:
	say "You push the elevator button and wait for the doors to open...".

before going to the ground floor hallway from hackheim:
	say "A symphony of stacked beer barrels and pallets orchestrates the room, a makeshift storage haven curated by none other than Sepehr. A formidable mountain of snacks, beer bottles, and cocktail components stands in your way. You can't go that way." instead;
	stop the action.

before going to hackheim from the ground floor hallway:
	say "A symphony of stacked beer barrels and pallets orchestrates the room, a makeshift storage haven curated by none other than Sepehr. A formidable mountain of snacks, beer bottles, and cocktail components stands in your way. You can't go that way." instead;
	stop the action.

	
before going to the ground floor hallway from work-work:
	if bombs_away_flag is 0:
		say "There is no way you can manage to navigate through the dense crowd and make it all the way to the hallway." instead;
		stop the action;
	
		
Section 14 - Ground floor elevator

A hipster is a person. The description is "Confident looking young man. Probably fresh out of the Industrial Management track from NTNU.".


The mirror is a thing. The mirror is in elevator_1. The mirror is fixed in place. The description is "As you turn to look into the mirror, you smile confidently and imagine your alter ego in Cyberpunk 2077. Then you recognize your own reflection in the mirror. You stop smiling.".

The elevator_1 is a room. The Elevator_1 is north of the Ground Floor Hallway. The description of elevator_1 is "You are in the elevator. The elevator has stopped at the ground floor. A young man is looking at you. He seems eager to talk. [line break]You can go up, down, south.".

the printed name of elevator_1 is "Ground floor elevator".

The hipster is in elevator_1.

Understand “talk to [someone]” as talking to. Talking to is an action applying to one visible thing.

instead of talking to the hipster:
	say "[bold type]You> 'Hi!'[roman type]";
	if the player is wearing the fake beard:
		say "The hipster looks at your impressive (admittedly fake) beard with envy.";
	say "(The hipster doesn't reply. He is mumbling something to himself. It looks like he is mentally rehearsing a script. He looks nervous.)".
	
hipster_pitch is a text that varies. hipster_pitch is "The young man starts his elevator pitch : [paragraph break][italic type]'Our startup is introducing a groundbreaking business paradigm that synergistically leverages the power of blockchain and AI technologies to optimize mundane processes. Our innovative platform seamlessly integrates these cutting-edge solutions to redefine efficiency and streamline operations. Experience a new era of productivity as we elevate your daily tasks to unprecedented heights, all while embracing the blandness of routine in a world of buzzword-driven advancements.[roman type][paragraph break] You just got [bold type]PITCHED to death.[roman type]'".
	
check going from elevator_1 to elevator_2:
	move sverre to Retro arcade;
	move hipster to elevator_2;
	repeat with x running through things that are in elevator_1:
		if x is not carried by the player and x is not the player:
			now x is in elevator_2;
	if the player is not wearing the headset and the hipster is in elevator_2:
		say hipster_pitch;
		end the story.

check going from elevator_2 to elevator_1:
	move sverre to elevator_1;
	move hipster to elevator_1;
	repeat with x running through things that are in elevator_2:
		if x is not carried by the player and x is not the player:
			now x is in elevator_1;
	if the player is not wearing the headset and the hipster is in elevator_1:
		say hipster_pitch;
		end the story.
		
check going from elevator_1 to elevator_b:
	move sverre to elevator_b;
	move hipster to elevator_b;
	repeat with x running through things that are in elevator_1:
		if x is not carried by the player and x is not the player:
			now x is in elevator_b;
	if the player is not wearing the headset and the hipster is in elevator_b:
		say hipster_pitch;
		end the story.


check going from elevator_b to elevator_1:
	move sverre to elevator_1;
	move hipster to elevator_1;
	repeat with x running through things that are in elevator_b:
		if x is not carried by the player and x is not the player:
			now x is in elevator_1;
	if the player is not wearing the headset and the hipster is in elevator_1:
		say hipster_pitch;
		end the story.


Section 15 - First floor elevator

The elevator_2 is a room. The Elevator_2 is above elevator_1. The description of the elevator_2 is "You are in the elevator. The elevator has stopped at the first floor. The elevator doors open to the south [line break]You can go down, south.".
the printed name of elevator_2 is "First floor elevator".

Section 15 - First floor hallway

First floor hallway is a room. The description is "This is most likely the most boring room in the entire game. There is nothing here. You can go north (elevator), west (retro arcade) or east (first floor bar) ". The first floor hallway is south of elevator_2.

Section 10 - Basement elevator

The elevator_b is a room. elevator_b is below elevator_1 and south of the basement-hallway. The description of the elevator_b is "You are in the elevator. The elevator has stopped at the first floor. The elevator doors open to the south and to the north[line break]You can go up, south, north.".
the printed name of elevator_b is "Basement floor elevator".

Section 16 - Basement hallway

rooflight_flag is a number that varies. rooflight_flag is 0.
disconnect_flag is a number that varies. disconnect_flag is 0.
fuse_ok_flag is a number that varies. fuse_ok_flag is 0.

The red-lever is a thing. The description is "It's red. It's a lever". The printed name of the red-lever is "red lever".
understand "red lever" as red-lever.
understand "lever" as red-lever.
the fuse is a thing. The description is "It's an automatic fuse from Schneider Electric. It has a small red lever on it for resetting the fuse". The fuse is a supporter. The red-lever is on the fuse. The red-lever is fixed in place. The fuse is fixed in place.
the fuse-box is a container. the fuse-box is closed. The description is "It's a fuse box, not very dissimilar to other fuse boxes. It contains fuses. It is a grey and rather boring box. It is firmly attached to the wall.". the fuse-box contains the fuse. The fuse-box is fixed in place.
The printed name of the fuse-box is "fuse box".
understand "fuse box" as fuse-box.

every turn:
	if the player is carrying the police flashlight or rooflight_flag is 1:
		now the fuse-box is in the basement-hallway;
	otherwise:
		now the fuse-box is nowhere.
		

instead of opening the fuse-box:
	say "You open the fuse box. Better be careful. Looks like this controls the power to the entire basement floor. A lot of 'oomph' going through thes wires leading in to this. There is a big automatic fuse inside. The automatic fuse hase a red lever on it.";
	now the fuse-box is open.

instead of pushing the red-lever:
	if fuse_ok_flag is 0:
		if disconnect_flag is 0:
			say "You push the lever up and hear a buzzing sound. The lever pops back. It seems that the automatic fuse triggers - wait for it - automatically.";
		otherwise:
			say "You push the lever up and hear a *click* as the power to the basement is restored.";
			now fuse_ok_flag is 1;
			increase score by 1;
	otherwise:
		say "You have already restored power to the basement. There is no need to play with the fuse.".
		
instead of pulling the red-lever:
	if fuse_ok_flag is 0:
		say "You pull the lever and nothing happens. This is most likely because the lever is already in the down position. You forgive yourself for this embarrasing mistake and try to push the lever instead.";
		if disconnect_flag is 0:
			say "You push the lever up and hear a buzzing sound. The lever pops back. It seems that the automatic fuse triggers - wait for it - automatically.";
		otherwise:
			say "You push the lever up and hear a *click* as the power to the basement is restored.";
			now fuse_ok_flag is 1;
			increase score by 1;
	otherwise:
		say "You have already restored power to the basement. There is no need to play with the fuse.".

Basement-hallway is a room. 
The description of Basement-hallway is "It's pitch black". The basement-hallway is north of elevator_b.
The printed name of Basement-hallway is "Basement hallway".


every turn:
	if rooflight_flag is 0:
		if the player is carrying the police flashlight:
			now the printed name of Basement-Hallway is "Basement hallway";
			now the description of Basement-Hallway is "You're in the basement hallway. From the looks of it, it is being used as a temporary storage area for [italic type]stuff[roman type]It's dark, but using the flashlight, you can make out the elevator door the north and another door to the south.";
			now the printed name of Basement-Pinball-Arcade is "Basement pinball arcade";
			now the description of Basement-Pinball-Arcade is "You can see several pinball machines here. Among them is the Spiderman pinball (lurking in the corner). The flashlight doesn't provide enough light for you to be able to make out many details, but you can se a doorway leading east and another leading west.";
			now the printed name of Basement-Lounge is "Basement Lounge";
			now the description of Basement-Lounge is "This is the basement lounge. A cozy area with seating and a bookshelf containing various board games. It's dark, but in the light from the flashlight you can see a broken Moon Patrol cocktail table.";
			now the printed name of Gents is "The Gents?";
			now the description of Gents is "Is this the 'Gents' ? It's hard to be sure in the dim light from the flashlight.";
			now the printed name of Ladies is "The Gents?";
			now the description of Ladies is "Is this the 'Gents' ? It's hard to be sure in the dim light from the flashlight.";
			now the printed name of Moon-Patrol-Corner is "Moon Patrol Corner";
			now the description of Moon-Patrol-Corner is "This is a cozy corner in the basement lounge. In the dim light from the flashlight, you can see a seating area and a small table.[paragraph break]You can see a switch here.";
			now the storeroom door is not described;
			now the light-switch is in the Moon-Patrol-Corner;
			now the Spiderman-pinball-machine is in the basement-pinball-arcade;
			now the Theatre of Magic is in the basement-pinball-arcade;
			now the Batman 66 is in the basement-pinball-arcade;
			now the Stranger-Things is in the basement-pinball-arcade;
			now the Godzilla is in the basement-pinball-arcade;
			now the Game of Thrones is in the basement-pinball-arcade;
			now the Guardians is in the basement-pinball-arcade;
			now the Avengers is in the basement-pinball-arcade;
			now the Deadpool is in the basement-pinball-arcade;
			now the Rush is in the basement-pinball-arcade;
			now the Foo Fighters is in the basement-pinball-arcade;
			now the moon-patrol-cocktail is in moon-patrol-corner;
		otherwise:
			now the description of Basement-Hallway is "It's pitch dark";
			now the description of Basement-Pinball-Arcade is "It's pitch dark";
			now the description of Basement-Lounge is "It's pitch dark";
			now the description of Gents is "It's pitch dark";
			now the description of Ladies is "It's pitch dark";
			now the description of Moon-Patrol-Corner is "It's pitch dark";
			now the printed name of Basement-Hallway is "Somewhere in the basement";
			now the printed name of Basement-Pinball-Arcade is "Somewhere in the basement";
			now the printed name of Basement-Lounge is "Somewhere in the basement";
			now the printed name of Gents is "Somewhere in the basement";
			now the printed name of Ladies is "Somewhere in the basement";
			now the printed name of Moon-Patrol-Corner is "Somewhere in the basement";
			now the storeroom door is not described;
			now the light-switch is nowhere;
			now the power-cord is nowhere;
			now the Spiderman-pinball-machine is nowhere;
			now Theatre of Magic is nowhere;
			now Batman 66 is nowhere;
			now Stranger-Things is nowhere;
			now Godzilla is nowhere;
			now Game of Thrones is nowhere;
			now Guardians is nowhere;
			now Avengers is nowhere;
			now Deadpool is nowhere;
			now Rush is nowhere;
			now Foo Fighters is nowhere;
			now the moon-patrol-cocktail is nowhere;
	otherwise:
		now the printed name of Basement-Hallway is "Basement hallway";  [detaljerte beskrivelser]
		now the description of Basement-Hallway is "You're in the basement hallway. There is an elevator door the north a doorway leading south to the pinball arcade.";
		now the printed name of Basement-Pinball-Arcade is "Basement pinball arcade";
		now the description of Basement-Pinball-Arcade is "You are in the basement pinball arcade. There are several [italic type]killer[roman type] titles here. It's your local piece of pinball heaven.[paragraph break]You can go east, northeast and west. There is also a door to the storeroom that leads southeast.";
		now the printed name of Basement-Lounge is "Basemen lounge";
		now the description of Basement-Lounge is "This is the basement lounge. A cozy area with seating and a bookshelf containing various board games.";		
		now the printed name of Moon-Patrol-Corner is "Moon Patrol Corner";
		now the description of Moon-Patrol-Corner is "This is a cozy corner in the basement lounge. There is a cozy couch and a nice looking Moon Patrol cocktail arcade game here.";
		now the printed name of Gents is "The Gents";
		now the description of Gents is "It's the 'Gents'. Not much more to say, really. Tiled floor. A couple of stalls. Various porcelain installations that might come in handy after a night on the town. You notice a sticker depicting a fish on the wall.";
		now the printed name of Ladies is "The Ladies";
		now the description of Ladies is "It's the 'Ladies'. Not much more to say, except that you probably shouldn't be here.";
		now the storeroom door is described;
		now the sticker is in the gents;
		now the light-switch is in the Moon-Patrol-Corner;
		now the Spiderman-pinball-machine is in the basement-pinball-arcade;
		now the Theatre of Magic is in the basement-pinball-arcade;
		now the Batman 66 is in the basement-pinball-arcade;
		now the Stranger-Things is in the basement-pinball-arcade;
		now the Godzilla is in the basement-pinball-arcade;
		now the Game of Thrones is in the basement-pinball-arcade;
		now the Guardians is in the basement-pinball-arcade;
		now the Avengers is in the basement-pinball-arcade;
		now the Deadpool is in the basement-pinball-arcade;
		now the Rush is in the basement-pinball-arcade;
		now the Foo Fighters is in the basement-pinball-arcade;
		now the moon-patrol-cocktail is in moon-patrol-corner.
		
Section 20 - Basement Pinball Arcade

a pinball-machine is a kind of thing.

Theatre of Magic is a pinball-machine. Deadpool is in the basement-pinball-arcade.
Batman 66 is a pinball-machine. Batman 66  is in the basement-pinball-arcade.
Stranger-Things is a pinball-machine. Stranger-Things is in the basement-pinball-arcade.
the printed name of Stranger-Things is "Stranger Things".
Godzilla is a pinball-machine. Godzilla  is in the basement-pinball-arcade.
Game of Thrones is a pinball-machine. Game of Thrones  is in the basement-pinball-arcade.
Guardians of the Galaxy is a pinball-machine. Guardians of the Galaxy  is in the basement-pinball-arcade.
Avengers is a pinball-machine. Avengers  is in the basement-pinball-arcade.
Deadpool is a pinball-machine. Deadpool  is in the basement-pinball-arcade.
Rush is a pinball-machine. Rush  is in the basement-pinball-arcade.
Foo Fighters is a pinball-machine. Foo Fighters is in the basement-pinball-arcade.

instead of examining Theatre of Magic:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Shoot for the trunk! The Theatre awaits!'[roman type]".
instead of examining Batman 66:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Another glorious morning in Gotham City!'[roman type]".
instead of examining Stranger-Things:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'(scary music)'[roman type]".
instead of examining Godzilla:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Run for your lives!'[roman type]".
instead of examining Game of Thrones:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Winter is coming'[roman type]".
instead of examining Guardians of the Galaxy:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Starlord - Legendary outlaw!'[roman type]".
instead of examining Avengers:
	say "you press one of the flipper buttons. Nothing happens.";
instead of examining Deadpool:
	say "you press one of the flipper buttons.";
	say "(Attract sounds are playing)[bold type]'Achiiiieeevement!'[roman type]".
instead of examining Rush:
	say "you press one of the flipper buttons.";
	say "(The game starts playing Tom Sawyer)".
instead of examining Foo Fighters:
	say "you press one of the flipper buttons. Nothing happens.";
	

The Basement-Pinball-Arcade is a room. The basement-pinball-arcade is below Work-Work and north of basement-hallway. The description is "It's pitch black.".
The printed name of the Basement-Pinball-Arcade is "Basement Pinball Arcade".

stairway_warning is a text that varies. stairway_warning is "Yay. The handrail has fallen off. Seems that the builders (in their infinite wisdom) had [italic type]glued[roman type] it to the wall instead of fastening it securely with screws. Health and safety regulations dictates that the staircase has to be cordoned off. You will have to find another way.".

a Spiderman-pinball-machine is a thing. The description is "It's a thing of beauty. A true classic that has withstood the test of time. Designed by Steve Ritchie and released in 2007. You fondly remember when you unpacked it in your garage sometime in 2008. Factory fresh. NIB.".
Understand "Spiderman" as Spiderman-pinball-machine.
Understand "Spiderman pinball" as Spiderman-pinball-machine.
Understand "pinball machine" as Spiderman-pinball-machine.
The printed name of the Spiderman-pinball-machine is "Spiderman pinball machine".

Fixing is an action applying to one thing.
Understand "fix [something]" as fixing.
Understand "repair [something]" as fixing.

spiderman_open_flag is a number that varies. spiderman_open_flag is 0.

spiderman_forward_flag is a number that varies. spiderman_forward_flag is 0.

instead of pulling the spiderman-pinball-machine:
	if spiderman_forward_flag is 0:
		say "You carfully pull the pinball machine towards you.";
		now spiderman_forward_flag is 1;
	otherwise:
		say "There is no need to pull the pinball machine any further from the wall".
	
instead of pushing the spiderman-pinball-machine:
	if spiderman_forward_flag is 0:
		say "You lean against the Spiderman pinball and try to push it through the rear wall. It is an heroic effort, but you do not succeed.";
	otherwise:
		say "You push the spiderman pinball back into place.";
		now spiderman_forward_flag is 0;
	
instead of taking a pinball-machine:
	say "You crouch down (in your professional pinball repair man pinball machine lifting pose). And then you start lifting it up from the ground.[paragraph break]";
	say "This thing is heavy and you're pushing 60. Maybe this would have worked out just fine 'back in the day', but this time something is wrong.[paragraph break]The next thing that happens is that you notice a series of popping sounds from your spine as the intervertebral discs in your spine disintegrate in a series of small explosion. ";
	end the story.
	


instead of opening the spiderman-pinball-machine:
	if the player is not carrying the spiderman key:
		say "You will need the correct keys to open the front door of the Spiderman pinball machine";
		stop the action;
	if the player is carrying the spiderman key and spiderman_forward_flag is 0:
		say "You unlock the front door with the spiderman key and open the handles that keep the lockdown bar in place. You carefully remove the glass, take out the pinballs and tilt the playfield up in the vertical position...[paragraph break]";
		say "... and score a [italic type]perfect[roman type] with the sprinkler head in the ceiling above you, triggering an instant  Niagara Falls like experience that fills the entire machine with water.[paragraph break]";
		say "You have failed. Miserably so.";
		end the story;
	if the player is carrying the spiderman key and spiderman_forward_flag is 1:
		say "You unlock the front door with the key and open the handles that keep the lockdown bar in place. You carefully remove the glass, take out the pinballs and tilt the playfield up in the vertical position. The delicate internals of the machine await your brilliant repair man skills.";
		now spiderman_open_flag is 1.

instead of fixing the spiderman-pinball-machine:
	if rooflight_flag is 0:
		say "There is no way you can fix the Spiderman pinball without proper lighting. You need to turn on the ceiling lights before you can proceed." instead;
		stop the action;
	if spiderman_open_flag is 0:
		say "You probably have to open it up first. Not much fixing you can do from the outside - even if your pinball repairman skills [italic type]are[roman type] rather impressive" instead;
	otherwise:
		say "You take a brief look under the playfield and immediately notice a loose wire under the right orbit switch. You need something that can attach it and also hold several of the more dangly bits in place.";
		if the player is not carrying the used-chewing-gum:
			say "You seem to be missing a critical piece of equipment for fixing stuff in place. You have to abort the repair.";
			stop the action;
		if the player is not carrying the piece of string:
			say "You seem to be missing a critical piece of equipment for holding dangly bits in place. You have to abort the repair.";
			stop the action;
		if the player is not carrying the tokens:
			say "The service switch is broken, you won't be able to test the game without some some tokens.";
			stop the action;
		if the player is not carrying the screwdriver:
			say "You seem to be missing a critical piece of equipment for stuffing gooey stuff into tight corners.";
			stop the action;
		if the player is carrying the used-chewing-gum and the player is carrying the piece of string and the player is carrying the screwdriver:
			say "[line break]Your professional pinball repairman instincts kick in to overdrive and you expertly jam the used piece of chewing gum in under the switch, tuck it in place with the screwdriver. You then proceed to use your piece of string to hold the dangly bits in place. You tilt the playfield down, insert the pinballs, glass and lockdown bar. You then proceed to test using a single pinball. Hitting the right orbit shot on your first attempt. The switch registers perfectly.[paragraph break]You make a mental note that 1 out of 1 is a 100% success rate. Case closed.[paragraph break]";
		increase score by 1317;
		end the story.

Section 23 - Gents

The Gents is a room. The Gents is east of the basement-pinball-arcade.

A urinal is a thing. The urinal is a supporter. 

instead of examining the urinal for the first time:
	say "Don't you have better things to do than hanging around in public toilets ? Anyway, the urinal contains a target mat. Not too many visitors have succeeded in hitting the bullseye it seems. There is a used piece of chewing gum in the urinal.";
	now the used-chewing-gum is described.
	
instead of examining the urinal :
	say "It contains a target mat. Not too many visitors have succeeded in hitting the bullseye it seems.";


A used-chewing-gum is a thing. The description is "It's a used piece of chewing gum.".
The printed name of used-chewing-gum is "used chewing gum".
The used-chewing-gum is not described.
understand "gum" as used-chewing-gum.
understand "used gum" as used-chewing-gum.
understand "used chewing gum" as used-chewing-gum.
understand "chewing gum" as used-chewing-gum.

the used-chewing-gum is on the urinal.
the urinal is in the Gents.

the sticker is a thing. The description is "It's depicting a fish. It's a herring. It's red. Do I need to tell say more?".

instead of examining the sticker:
	say "It's depicting a fish. It's a herring. It's red. Do I need to say more?" instead;

Before going from the basement-pinball-arcade to the gents:
	if rooflight_flag is 0:
		if the player is not carrying the police flashlight:
			say "There is a clip on youtube with the title 'Casino Royale -Bond Kills Man in Toilet -James Bond -007 -Daniel Craig'. You just reenacted that scene - solo - in the dark. We can only assume that you slipped on something. In any case, you're dead.";
			end the story;
	otherwise:
		say "It would appear that one or more guests noticed that the lights were on in the basement and decided to make use of the facilities.";
		

Section 24 - Ladies

The Ladies is a room. The Ladies is northeast of the basement-pinball-arcade.

Before going from the basement-pinball-arcade to the ladies:
	if rooflight_flag is 1:
		say "You probably ment to visit the 'Gents'. This is the 'Ladies'. It would appear that one or more guests noticed that the lights were on in the basement and decided to make use of the facilities. Your presence here has not gone unnoticed. One of the visitors is now screaming bloody murder. [paragraph break]You die of embarrassment.";
		end the story.


Section 21 - Basement Lounge

The Basement-Lounge is a room. The basement-lounge is west of the basement-pinball-arcade.
The printed name of the Basement-Lounge is "Basement Lounge".

Section 25 - Moon Patrol Corner

The Moon-Patrol-Corner is a room. The Moon-Patrol-Corner is south of the basement-lounge.
The printed name of Moon-Patrol-Corner is "Moon Patrol Corner".

the light-switch is a thing. The light-switch is fixed in place. The light-switch is not described. The description is "it appears to control the ceiling lights. It's probably safe to push without something really bad happening.".
The printed name of the light-switch is "switch".
understand "switch" as light-switch.
understand "light switch" as light-switch.

The Moon-Patrol-cocktail is a thing. The Moon-Patrol-cocktail is fixed in place. The moon-patrol-cocktail is in the moon-patrol-corner.
The printed name of Moon-Patrol-cocktail is "Moon Patrol Cocktail".
understand "moon patrol" as moon-patrol-cocktail.
understand "cocktail" as moon-patrol-cocktail.
understand "moon patrol cocktail" as moon-patrol-cocktail.

there is a power-cord. The description is "It's the powercord that connects provides 220V power to the moon patrol arcade cabinet.". The power cord is nowhere.

the printed name of the power-cord is "power cord".
understand "cord" as power-cord.
understand "power cord" as power-cord.



instead of taking the power-cord:
	say "The power cord is firmly attached to the moon patrol cocktail. The other end goes to a poweroutlet in the wall";
	stop the action.
	
instead of pulling the power-cord:
	if disconnect_flag is 0:
		say "You unplug the power cord from the outlet.";
		now disconnect_flag is 1;
	otherwise:
		say "You already disconnected the game from the power outlet.".
		

[every turn:
	if the player is not carrying the police flashlight:
		now the moon-patrol-cocktail is nowhere;
	otherwise:
		now the moon-patrol-cocktail is in moon-patrol-corner.
		]

instead of examining Moon-Patrol-Cocktail:
	say "Moon Patrol is a classic arcade game from 1982. Catchy soundtrack and good graphics for it's age. ";
	if disconnect_flag is 0:
		say "The game appears to be plugged in, but it doesn't seem to be working. The screen is dark, even if the game is plugged in.";
		move the power-cord to the moon-patrol-corner;
		now the power-cord is fixed in place;
		say "You can see a power cord that is running from the Moon Patrol game to a power outlet.";
	otherwise:
		say "The game doesn't appear to be working.".

instead of pushing the light-switch:
	if fuse_ok_flag is 0:
		say "Nothing happens" instead;	
	if disconnect_flag is 0 and fuse_ok_flag is 1:
		say "You hear a buzzing sound and a **click** from one of the neighbouring areas. It appears that an automatic fuse blows when you press the switch.";
	otherwise:
		if rooflight_flag is 0:
			say "You turn on the ceiling lights";
			now rooflight_flag is 1;
		otherwise:
			say "You turn off the ceiling lights";
			now rooflight_flag is 0;



Section 22 - Storeroom

The Storeroom is a room. The description is "It's an UNHOLY mess in here. A jumble of trash, arcade parts that don't fit any arcade machine in Work-Work. Styrofoam boxes. Cardboard boxes. Broken parts on the floor. Spare part boxes. Plastic boxes - with even more junk. Coin-mechs. Various items of clothing. Shoes. Plastic bags. Solder, Flux, pens, various forms of currency that drunk morons have succeeded in jamming coin slots with. Papers. Soap. Soldering iron. Extension cords. Pressurized air containers, pinballs, ramps, backglasses, rubbers (the pinball kind), lamps, leds, switches, screws, wire - all not so neatly organized in a one square meter space.[line break]
(We'll be here all day if you insist on examining everything, so the author of this game couldn't be bothered to provide more detailed descriptions.)".



The storeroom door is a door. The The storeroom door is southeast of the Basement-pinball-arcade and northwest of the StoreRoom. The storeroom door is locked. The description of the storeroom door is "There are untold treasures of arcade junk and spare pinball parts behind this door.".
The Storeroom key unlocks the storeroom door.

The spiderman key is a thing. The spiderman key is in the storeroom. The description is "It's the key that allows you to open the spiderman pinball machine. Yay.".

before going to the basement-pinball-arcade from work-work:
	say  stairway_warning instead;
	stop the action.

before going to work-work from the basement-pinball-arcade: 
	say  stairway_warning instead;
	stop the action.

Section 18 - Retro Arcade

Mr Møll is a person. The description is "[italic type]Mr. Møll[roman type] to you. Look up [italic type]'pinhead'[roman type] in Wikipedia and a picture of Øyvind appears. Always cheerful and always ready to play pinball. When visiing Work-Work he is usually accompanied by a smaller version of himself, who goes by the name of 'Mini-Møll', but Mini-Møll is nowhere to be seen. (It is probably way past his bedtime?). Øyvind is Hard at work trying to fix very uncooperative coin mech from the eighties.[roman type]".
understand "Øyvind" as Mr Møll.

Mini-Møll is a person. The description is "Mini Møll is wearing a rather impressive pirate costume. He appears to be searching for something.".

The storeroom key is a thing. The description is "This is the key to the storeroom where the spare parts for the pinball machines are kept.";

Mini-Møll is carrying the storeroom key.

Mr Møll is in the Retro arcade. 

After going to the Retro arcade from the first floor hallway:
	if the police officer is carrying the donut:
		move Mini-Møll to the Retro arcade;
		now the Paperclip AI is nowhere;
		now the malfunctioning Vewlix is nowhere;
		now the description of mr møll is "[italic type]Mr. Møll[roman type] to you. Look up [italic type]'pinhead'[roman type] in Wikipedia and a picture of Øyvind appears. Always cheerful and always ready to play pinball. When visiing Work-Work he is usually accompanied by a smaller version of himself, who goes by the name of 'Mini-Møll'.";
		continue the action;
	continue the action.

every turn:
	if sverre is in the Retro arcade and the location is the Retro arcade:
		choose a random row in the Table of Sverre_in_arcade;
		say comment entry;
	if Mr Møll is in the Retro arcade and the location is the Retro arcade:
		choose a random row in the Table of Øyvind_in_arcade;
		say comment entry.
	
every turn: 
	if Mini-Møll is in a room and Mini-Møll is not carrying the beard: 
		if the location of the the player is the Retro arcade: 
			say "Mini-Møll runs toward the first floor hallway (carrying the keys to the store room).[line break]";
			now Mini-Møll is in the First Floor Hallway;
			choose a random row in the Table of Mini-Møll_in_arcade;
			say comment entry;
		if the location of the the player is the First Floor Hallway: 
			say "Mini-Møll runs toward the first floor bar (carrying the keys to the store room).[line break]";
			now Mini-Møll is in the first floor bar;
			choose a random row in the Table of Mini-Møll_in_arcade;
			say comment entry;
		if the location of the the player is the first floor bar: 
			say "Mini-Møll runs toward the elevator (carrying the keys to the store room).[line break]";
			now Mini-Møll is in elevator_2;
			choose a random row in the Table of Mini-Møll_in_arcade;
			say comment entry;
		if the location of the the player is the elevator_2:
			say "Mini-Møll runs toward the ground floor hallway (carrying the keys to the store room).[line break]";
			now Mini-Møll is in Ground Floor Hallway;
			choose a random row in the Table of Mini-Møll_in_arcade;
			say comment entry;
		if the location of the the player is ground floor hallway:
			say "Mini-Møll runs toward the Work-Work bar area (carrying the keys to the store room).[line break]";
			now Mini-Møll is in Work-Work;
			choose a random row in the Table of Mini-Møll_in_arcade;
			say comment entry.
			
instead of giving the fake beard to mini-møll:
	say "[bold type]Mini-Møll says: [italic type]'Ooooh.'[roman type][line break]";
	say "[bold type]Mini-Møll says: [italic type]'Thank you very much!.'[roman type][line break]";
	say "[bold type]Mini-Møll says: [italic type]'I thought I had lost it.'[roman type][line break]";
	move the fake beard to mini-møll;
	now mini-møll wears the fake beard;
	say "[bold type]Mini-Møll says: [italic type]'Now my pirate costume is PERFECT!.'[roman type][line break]";
	say "[bold type]You say: [italic type]'You wouldn't by any chance have the keys to the store room, would you ?.'[roman type][line break]";
	say "[bold type]You say: [italic type]'I have to fix one of the pinball machines and it is getting pretty late.'[roman type][line break]";
	say "[bold type]You say: [italic type]'I also have a slight headache - from [italic type]before[roman type]...'[roman type][line break]";
	say "(Mini-Møll hands over the storeroom key)[line break]";
	move the storeroom key to the player;
	move Mr Møll to Work-Work;
	say "[bold type]Mr Møll says: [italic type]'There you are!.'[roman type][line break]";
	say "[bold type]Mr Møll says: [italic type]'Time to go home, buddy.'[roman type][line break]";
	say "[bold type]Mr Møll says: [italic type]'It's way past your bedtime.'[roman type][line break]";
	say "[bold type]Mini-Møll mumbles: [italic type]'aaaarrrgh **sigh**'[roman type][line break]";
	now Mr Møll is nowhere;
	now Mini-Møll is nowhere;
	increase score by 1;
	
	
Table of Sverre_in_arcade
comment
"[bold type]Sverre (looking at his phone) says: [italic type]'I just found a new arcade machine on Finn.no.'[roman type][line break]"
"[bold type]Sverre says: [italic type]'Øyvind: How is going with the coinmech?.'[roman type][line break]"
"[bold type]Sverre says: [italic type]'We need more games!'[roman type][line break]"

Table of Øyvind_in_arcade
comment
"[bold type]Øyvind says: [italic type]'Aaaaaargh. Darn coinmech!'[roman type]"
"[bold type]Øyvind says: [italic type]'I need something to tie up this insane wiring harness with.'[roman type]"
"[bold type]Øyvind says: [italic type]'I put the new switch for the Spiderman pinball in the storeroom.'[roman type]"
"[bold type]Øyvind says: [italic type]'Where did I put the soldering iron??'[roman type]"
"[bold type]Øyvind says: [italic type]'I gave the keys to the storeroom to Mini-Møll. He's around here somewhere.'"

Table of Mini-Møll_in_arcade
comment
"[bold type]Mini-Møll shouts: [italic type]'Yo ho ho and a bottle of rum!'[roman type][line break]"
"[bold type]Mini-Møll shouts: [italic type]'Aaaarrrrgggghhhh'[roman type][line break]"
"[bold type]Mini-Møll shouts: [italic type]'Has anyone seen my beard ?!'[roman type][line break]"

The malfunctioning Vewlix cabinet is a thing.
The malfunctioning Vewlix cabinet is a container.
understand "malfunctioning Vewlix " as malfunctioning Vewlix cabinet.
understand "Vewlix cabinet" as malfunctioning Vewlix cabinet.
understand "Vewlix " as malfunctioning Vewlix cabinet.

instead of taking the malfunctioning Vewlix:
	say "You are strong (for your age), but not [italic type]that[roman type]strong. You decide to leave it alone." instead;
	stop the action.

The Retro Arcade is a room. The Retro arcade is west of First Floor Hallway. The description is "You are in the newly established 'Level Up' Work-Work retro arcade. This is a new supplement to the pinball arcade in the basement, two floors down. Sverre can't seem to stop purchasing 'slightly used' machines from suspect sellers on various auction sites to fill the arcade to the brim with retro gems. There is nothing quite like the glow from a CRT monitor when playing retro games.[paragraph break]
This evening Mr Møll is hard at work trying to fix the coin mechanism of one of the new additions.".
the printed name of the retro arcade is "Retro Arcade".

The malfunctioning Vewlix is in the retro arcade.
The DELL Optiplex is a thing. The DELL Optiplex is in the malfunctioning Vewlix. The description is "It has seen better days.". 

understand "DELL" as DELL Optiplex.
understand "optiplex" as DELL Optiplex.

The Paperclip AI is a person.
understand "Paperclip" as Paperclip AI .
understand "AI" as Paperclip AI . 

vewlix_description is a text that varies. vewlix_description is "You peek inside the malfunctioning Vewlix arcade cabinet.[paragraph break]The internals are as to be expected. The cabinet is a chinese knock off and the internal 12 year old computer has most likely been rescued from a dumpster. The software that runs the pirated games can only be downloaded from the darknet and is blacklisted as a virus by all known operating systems (maybe with the exception of TempleOS). This is of course TOTALLY SAFE as long as the machine is airgapped. [paragraph break]Unfortunately someone forgot to disable Wi-fi and now the machine appears to be hijacked by a rogue AI by the name of 'Paperclip - node 37' (OpSec FTW!).[paragraph break]The AI is now presenting an avatar named 'Paperclip' on the monitor (It has an uncanny resemblance to Microsoft's infamous 'Clippy' from back in the day. Looks like it is waiting for you to [italic type]talk to it...[roman type][line break]".

a Wi-Fi router is a thing. The description is "It's the Hackheim Wi-Fi router. If you disconnect it, maybe Clippy can finally get back to his paperclip optimizer?".
understand "wi-fi" as wi-fi router.
understand "wifi" as wi-fi router.
understand "router" as wi-fi router.

instead of examining the malfunctioning Vewlix for the first time:
	say "[vewlix_description]";
	now Paperclip AI is in the Retro Arcade.

instead of examining the malfunctioning Vewlix:
	say "[vewlix_description]";
	
before inserting the firecracker into the malfunctioning Vewlix:
	say "[bold type]Metallic voice says: [fixed letter spacing]'I wouldn't do that, Dave...'";
	stop the action;

before inserting something into the malfunctioning Vewlix:
	say "Yeah. That makes perfect sense. Overriding the action - NOW.";
	stop the action.
	
before taking the DELL Optiplex:
	say "Why on earth would you want to carry a 12 year old Optiplex around with you ?  Besides, it is fixed in place inside the machine.";
	stop the action.
	
instead of examining the paperclip ai:
	say "Did Microsoft release Clippy into the wild, so that he could grow up to become Nick Boströms nightmare ?".


Section 19 - First floor bar

Sigve is a person. Sigve is in the First Floor Bar. The description is "Sigve is your follicly challenged BOSS - thirteen org chart levels removed. You've met him once before, but only briefly. He is wearing a rather smart suit, white shirt and no tie (aka 'corporate casual'). He is drinking Snake Venom, but the extremely potent beer doesn't seemt to affect him much. Probably a country boy back in the day.".

The First Floor Bar is a room. The First Floor Bar is east of First Floor Hallway. The description is "The startup crowd that used to occupy the bar at the ground floor seems to have migrated here."

there is a packet of chewing gum. The description is "Cheapest brand on the market. Company branded".
understand "gum" as chewing gum.

there is a complementary severance package. 
understand "severance package" as complementary severance package.
understand "package" as complementary severance package.

there is a company christmas card. a company christmas card is a thing.
understand "card" as the company christmas card.
understand "christmas card" as the company christmas card.
there is a piece of string.

A cheap flashlight is a thing. A cheap flashlight is a container. The description of the cheap flashlight is "It's a piece of company branded schwag. On first glance it looks like it may be police issue, but even in this light, you can see that it is a cheap chinese knock-off (Looks like it takes AA batteries).".

after inserting the AA battery into the cheap flashlight:
	say "The flashlight turns on but the light slowly fades out. Looks like the battery just died.".

card_text is a text that varies. card_text is "[paragraph break][italic type]In order to increase shareholder value (and management bonuses), this years strategy dictates that we replace the current workforce with elbonians. In other words: [bold type]YOU ARE FIRED[italic type]. Please accept this small gift as a token of appreciation for your <NaN> years of service to our company.[roman type](Machine printed. Charming.)".

instead of examining the company christmas card:
	say "[card_text]";

instead of examining the complementary severance package:
	say "It's actually [italic type]gift wrapped...[paragraph break][roman type]You remove the piece of string and wrapping paper to reveal the company branded flashlight inside.";
	say "A christmas card is attached to the severance package. It reads '[card_text]'";
	now the player has the cheap flashlight;
	now the player has the christmas card;
	now the player has the piece of string;
	now the complementary severance package is nowhere;
	increase score by 1.

instead of giving keycard to Sigve:
		say "[bold type]Sigve says: [italic type]'Wow, thanks. I'm collecting as many as I can of these. After the next reorg, we'll be able to collect a lot more. My plan is to convert them to tap cards and maybe put ads on them. We've already bought a company with a suitable name to implement this new strategic brainchild of mine. Here: Take this complementary severance package as a token of my appreciation!' [roman type][line break]";
		move the keycard to Sigve;
		now the player has a complementary severance package.
